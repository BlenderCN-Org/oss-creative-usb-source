# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libvpx
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.0
pkgrel=5
pkgdesc="The VP8 Codec SDK (mingw-w64)"
arch=('any')
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-yasm")
url=('http://www.webmproject.org/')
source=(https://webm.googlecode.com/files/${_realname}-v${pkgver}.tar.bz2
        0001-enable-shared-on.mingw.patch
        0002-no-pthreads.mingw.patch
        0003-cross-with-native-binutils.mingw.patch
        0004-implib.mingw.patch
        0005-fix-exports.mingw.patch
        0006-instll-implib.mingw.patch
        0007-fix-mingw-w64-strtok.mingw.patch
        0007-winxp-dont-have-strtok_s.patch
        0008-fix-ln-on-install.mingw.patch)
options=('strip' 'staticlibs')
md5sums=('14783a148872f2d08629ff7c694eb31f'
         '91e8e57bd26acb1acf5b0df5fbf36532'
         '8799da31817a2d0d6a3db4717e80ac24'
         '76677cf7a34fa84db945ecca2e8efaed'
         '33f066c4d11a928bad02f5877b36d148'
         'ccfc3e6d0bd8aeea89b29605ad783242'
         '816bb55c8593e639c2b083bf13509542'
         '5b1411bb3a36f8f126a6da74d3dcb305'
         'b21aaa85f12c88f83311d3cec1c29a04'
         '4fa75aed4f5929a6630f59b2b3114ff7')

prepare() {
  cd ${srcdir}/$_realname-v$pkgver

  patch -Np1 -i ${srcdir}/0001-enable-shared-on.mingw.patch
  patch -Np1 -i ${srcdir}/0002-no-pthreads.mingw.patch
  patch -Np1 -i ${srcdir}/0003-cross-with-native-binutils.mingw.patch
  patch -Np1 -i ${srcdir}/0004-implib.mingw.patch
  patch -Np1 -i ${srcdir}/0005-fix-exports.mingw.patch
  patch -Np1 -i ${srcdir}/0006-instll-implib.mingw.patch
  #patch -Np1 -i ${srcdir}/0007-fix-mingw-w64-strtok.mingw.patch
  #patch -Np1 -i ${srcdir}/0007-winxp-dont-have-strtok_s.patch
  patch -Np1 -i ${srcdir}/0008-fix-ln-on-install.mingw.patch
}

build() {
  if [ ${MINGW_CHOST} = "i686-w64-mingw32" ]; then
    _targetarch="x86-win32-gcc"
  else
    _targetarch="x86_64-win64-gcc"
  fi
  mkdir -p $srcdir/build-$MINGW_CHOST
  cd "$srcdir/build-$MINGW_CHOST"
  ../$_realname-v$pkgver/configure \
    --prefix=${MINGW_PREFIX} \
    --target=${_targetarch} \
    --enable-vp8 \
    --enable-vp9 \
    --enable-runtime-cpu-detect \
    --enable-postproc \
    --enable-pic \
    --enable-shared \
    --enable-static \
    --disable-examples \
    --disable-docs \
    --disable-install-docs \
    --disable-install-srcs

  for _ff in *.mk; do
    sed -i "s/HAVE_GNU_STRIP=yes/HAVE_GNU_STRIP=no/g" $_ff
  done
  make -j1
}

package() {
  cd "${srcdir}/build-$MINGW_CHOST"
  make DIST_DIR="${pkgdir}${MINGW_PREFIX}" install

  rm ${pkgdir}${MINGW_PREFIX}/bin/libvpx{.dll,-1.3}*
}
