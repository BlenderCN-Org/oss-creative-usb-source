# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=glade
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.18.3
pkgrel=2
pkgdesc="User interface builder for GTK+ and GNOME (mingw-w64)"
arch=('any')
url="http://glade.gnome.org"
license=("GPL" "LGPL")
options=('strip' '!debug' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
        "${MINGW_PACKAGE_PREFIX}-libxml2"
        "${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-gtk-doc"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            "${MINGW_PACKAGE_PREFIX}-python2-gobject2"
            'intltool'
            'itstool'
            'docbook-xsl')
optdepends=("${MINGW_PACKAGE_PREFIX}-python2: Python widgets support"
            #'devhelp: help browser'
            )
url="http://glade.gnome.org/"
install=glade-${CARCH}.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        0004-give-cc-to-gir-scanner.all.patch
        0005-give-gtk-cflags-to-gir.patch
        0006-disable-gnome-help.patch
        0007-glade-3.16.1-no-undefined.patch
        0008-localedir-fix.patch
        0009-icon-rc-relative.patch)
md5sums=('6852d6286683728e0ea40ca9b5d2416f'
         'fb4d5a33bbfea33b6437c85a9ac567c8'
         '294197385457bdc58905a32b6d0e9e1e'
         '3ebefeb0d48ff89f7992e5046ce9f09c'
         '15aa17f408e551a19a5419d6db51a58a'
         '48f5c9d78b9140d7e677a28a6d620ce3'
         'f1c38e6e5ab0c24a40261fa02c4ec799')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/0004-give-cc-to-gir-scanner.all.patch
  patch -p1 -i ${srcdir}/0005-give-gtk-cflags-to-gir.patch
  patch -p1 -i ${srcdir}/0006-disable-gnome-help.patch
  patch -p1 -i ${srcdir}/0007-glade-3.16.1-no-undefined.patch
  patch -p1 -i ${srcdir}/0008-localedir-fix.patch
  patch -p1 -i ${srcdir}/0009-icon-rc-relative.patch
  autoreconf -fi
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  #cd "${srcdir}/${_realname}-${pkgver}"
  PYTHON=${MINGW_PREFIX}/bin/python2 \
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-introspection \
    --enable-shared \
    --disable-gtk-doc \
    --enable-gladeui \
    --disable-man-pages

  make VERBOSE=1
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
}
