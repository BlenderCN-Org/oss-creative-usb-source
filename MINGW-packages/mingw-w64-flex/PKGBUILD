# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=flex
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.5.39
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"
arch=('any')
url="http://flex.sourceforge.net"
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-m4" "${MINGW_PACKAGE_PREFIX}-libiconv" "${MINGW_PACKAGE_PREFIX}-gettext")
options=('staticlibs' 'debug' '!strip')
source=(http://downloads.sourceforge.net/sourceforge/flex/${_realname}-$pkgver.tar.bz2
        0002-dont-wait-on-child-since-there-is-no-fork.patch
        0003-define-htonl-htons-for_WIN32.patch
        0004-check-for-regex-library-for-MinGW-w64.patch
        0005-filter-c-hack-out-fork-and-pipe.patch
        0006-MinGW-w64-open-in-binary-mode.patch)
md5sums=('77d44c6bb8c0705e0017ab9a84a1502b'
         '1335914681e4cbdf81300f0f4fadad54'
         '13cf10d90a617f508d315ac47da895ed'
         '14d935f8b06a80d06702f30ab4411cf4'
         'b7df5389abcf6d9dff24b4355a0073e6'
         'ad8d0cd227f7b1f9a290301e8eb17d11')

prepare() {
  cd "${srcdir}"/${_realname}-$pkgver

  patch -p1 -i "${srcdir}"/0002-dont-wait-on-child-since-there-is-no-fork.patch
  patch -p1 -i "${srcdir}"/0003-define-htonl-htons-for_WIN32.patch
  patch -p1 -i "${srcdir}"/0004-check-for-regex-library-for-MinGW-w64.patch
  patch -p1 -i "${srcdir}"/0005-filter-c-hack-out-fork-and-pipe.patch
  patch -p1 -i "${srcdir}"/0006-MinGW-w64-open-in-binary-mode.patch

  # Setting this to 1 will make debugging mutliple concurrent parse traces
  # difficult, so when get to that stage may want comment this out.
  if check_option "debug" "y"; then
    sed -i "s,int yydebug;,int yydebug = 1;,g" parse.c
  fi

  # these tests used features removed in bison-2.6
  sed -i -e '/test-bison-yylloc/d' -e '/test-bison-yylval/d' tests/Makefile.in

  ./autogen.sh
}

build() {
  mkdir -p "${srcdir}"/build-${CARCH}
  cd "${srcdir}"/build-${CARCH}
  if check_option "debug" "y"; then
    CPPFLAGS="$CPPFLAGS -DYYDEBUG=1"
  fi
  ../${_realname}-$pkgver/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    --enable-static \
    --disable-shared
  make
}

check() {
  cd "${srcdir}"/build-${CARCH}

  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}

  make DESTDIR=$pkgdir install
  cp -f ${pkgdir}${MINGW_PREFIX}/bin/flex.exe ${pkgdir}${MINGW_PREFIX}/bin/flex++.exe
  cp -f ${pkgdir}${MINGW_PREFIX}/bin/flex.exe ${pkgdir}${MINGW_PREFIX}/bin/lex.exe

  install -Dm644 COPYING \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/license.txt
}

# Previous flex ports:
# http://optimate.dl.sourceforge.net/project/flex/flex/2.5.4.a/flex-2.5.4a.tar.bz2
# http://dfn.dl.sourceforge.net/project/gnuwin32/flex/2.5.4a-1/flex-2.5.4a-1-src.zip
