# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=latexila
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.14.3
pkgrel=1
arch=('any')
pkgdesc="LaTeX editor designed for the GNOME desktop (mingw-w64)"
options=(strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-gtksourceview3"
         "${MINGW_PACKAGE_PREFIX}-gtkspell3"
         "${MINGW_PACKAGE_PREFIX}-libgee"
         "${MINGW_PACKAGE_PREFIX}-gsettings-desktop-schemas")
makedepends=("${MINGW_PACKAGE_PREFIX}-vala"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
license=("GPL 2")
url="http://www.gnome.org"
install=latexila-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        001-win.patch)
sha256sums=('bdc46a52cba778646af05ade9ea7cc639e34f8aafdb640ad2217f21d067c7039'
            'a90bc6f730e79504c167c5d34c8781e2af6156867433ac1d47188f43ddf787be')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  echo "" > vapi/windows.vapi
  patch -p1 -b -V simple -z .orig -i ${srcdir}/001-win.patch

  autoreconf -sf
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"

  make DESTDIR=$pkgdir install
}
