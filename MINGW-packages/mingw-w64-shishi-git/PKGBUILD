# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=shishi
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r3570.9d2b6c8
pkgrel=1
pkgdesc="a GNU implementation of the Kerberos 5 network security system (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/shishi/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "wget" "git")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gnutls"
        "${MINGW_PACKAGE_PREFIX}-libidn"
        "${MINGW_PACKAGE_PREFIX}-libgcrypt"
        "${MINGW_PACKAGE_PREFIX}-libgpg-error"
        "${MINGW_PACKAGE_PREFIX}-libtasn1"
        )
source=("${_realname}"::"git+git://git.savannah.gnu.org/shishi.git"
        001-fix-search-libgcrypt.patch
        002-gnulib.patch
        003-no-shishid.patch)
md5sums=('SKIP'
         'f2b2114a4dddb11e240df88086d9d33e'
         '2aaba8e55f17fd7f0be22f81a61f8d74'
         'b5af8571cc41670bd017d5fe7e4b48d9')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  patch -p1 -i ${srcdir}/001-fix-search-libgcrypt.patch
  patch -p1 -i ${srcdir}/002-gnulib.patch
  patch -p1 -i ${srcdir}/003-no-shishid.patch
  cp -rf build-aux/snippet ${srcdir}/
  rm -f aclocal.m4
  WANT_AUTOMAKE=latest autoreconf -fi
  cp -rf ${srcdir}/snippet build-aux/
  cd ..

  cp -rf ${_realname} build-${MINGW_CHOST}
}

build() {
  cd build-${MINGW_CHOST}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static
  make update-po
  make -j1
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
