# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: AlexWMF <alexxwmf@gmail.com>

_realname=a52dec
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.7.4
pkgrel=1
pkgdesc="A free library for decoding ATSC A/52 streams"
url="http://liba52.sourceforge.net/"
arch=('any')
license=('GPL2')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=("http://liba52.sourceforge.net/files/${_realname}-${pkgver}.tar.gz"
        'a52dec-0.7.4-build.patch')
md5sums=('caa9f5bc44232dc8aeea773fea56be80'
         'fa16f224a7dceb7613824380abef0052')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/a52dec-0.7.4-build.patch"
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.in
  mv configure.in configure.ac
  ./bootstrap \
    --prefix=${MINGW_PREFIX}
}

build() {
  cd ${_realname}-${pkgver}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 liba52/a52_internal.h "${pkgdir}${MINGW_PREFIX}/include/a52dec/"
}
