# Maintainer: AlexWMF <alexxwmf@gmail.com>

_realname=tidyhtml
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
# obtain pkgver from http://tidy.cvs.sourceforge.net/tidy/tidy/src/version.h?view=markup
pkgver=1.46
pkgrel=1
pkgdesc="A tool to tidy down your HTML code to a clean style"
arch=("any")
url="http://tidy.sourceforge.net/"
license=('custom')
depends=()
makedepends=("cvs"
            "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=(!strip staticlibs !buildflags)

source=(${_realname}-${pkgver}.tar.bz2
        mappedio_sizecheck.patch
        version_fix.patch)
md5sums=('d770aaf482aab39aa4fcf733844927ea'
         '1110402477f0b7224b11f531fc4ba865'
         '705045379df25ed4325d865bbf954ad6')

mksource() {
  mkdir ${_realname}-${pkgver}
  cd ${_realname}-${pkgver}
  cvs -d:pserver:anonymous@tidy.cvs.sourceforge.net:/cvsroot/tidy login   #hit enter for password
  cvs -z3 -d:pserver:anonymous@tidy.cvs.sourceforge.net:/cvsroot/tidy co -P tidy
  cd ..
  tar -cvjf ${_realname}-${pkgver}.tar.bz2 ${_realname}-${pkgver}/*
}

prepare() {
  cd ${_realname}-${pkgver}
  patch -p0 -i ${srcdir}/mappedio_sizecheck.patch
  patch -p0 -i ${srcdir}/version_fix.patch
}

build(){
  cd ${_realname}-${pkgver}/tidy
  source build/gnuauto/setup.sh

  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd ${_realname}-${pkgver}/tidy
  make DESTDIR=$pkgdir install

  install -Dm644 htmldoc/license.html \
    "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE
}
