# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=lz4
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.5.0.r127
pkgrel=1
pkgdesc="Very fast lossless compression algorithm (mingw-w64)"
arch=('any')
url="https://code.google.com/p/lz4/"
license=("GPL2")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "subversion")
checkdepends=('diffutils')
options=('staticlibs' 'strip')
source=("${_realname}::svn+http://lz4.googlecode.com/svn/trunk/#revision=$pkgver"
        001-mingw-install-over-msys.patch)
md5sums=('SKIP'
         '99b3643a9fee86ee89e991a682edb3c5')

pkgver() {
  cd ${_realname}
  local _MAJOR=`sed -n '/define LZ4_VERSION_MAJOR/s/.*[[:blank:]]\([0-9][0-9]*\).*/\1/p' < lib/lz4.h`
  local _MINOR=`sed -n '/define LZ4_VERSION_MINOR/s/.*[[:blank:]]\([0-9][0-9]*\).*/\1/p' < lib/lz4.h`
  local _PATCH=`sed -n '/define LZ4_VERSION_RELEASE/s/.*[[:blank:]]\([0-9][0-9]*\).*/\1/p' < lib/lz4.h`
  local _REV="$(svnversion)"
  printf "%s.%s.%s.r%s" "${_MAJOR}" "${_MINOR}" "${_PATCH}" "${_REV//[[:alpha:]]}"

}

prepare() {
  cd ${_realname}
  patch -p1 -i ${srcdir}/001-mingw-install-over-msys.patch
  cd -

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  cp -rf "${srcdir}/${_realname}" "${srcdir}/build-${MINGW_CHOST}"
}

build() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make CC=${MINGW_PREFIX}/bin/gcc
}

check() {
  build-${MINGW_CHOST}/programs/lz4 /etc/profile profile.lz4
  build-${MINGW_CHOST}/programs/lz4 -d profile.lz4 profile
  diff -q /etc/profile profile
  rm profile

  cd "${srcdir}/build-${MINGW_CHOST}"
  make CC=${MINGW_PREFIX}/bin/gcc test
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make CC=${MINGW_PREFIX}/bin/gcc DESTDIR="$pkgdir" PREFIX=${MINGW_PREFIX} install
}
