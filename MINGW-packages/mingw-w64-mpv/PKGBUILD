# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: James Ross-Gowan <rossymiles@gmail.com>

_realname=mpv
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.8.0
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (mingw-w64)"
url="http://mpv.io"
arch=('any')
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-ffmpeg"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libdvdread"
         #"${MINGW_PACKAGE_PREFIX}-libcdio-paranoia"
         "${MINGW_PACKAGE_PREFIX}-enca"
         "${MINGW_PACKAGE_PREFIX}-libguess"
         "${MINGW_PACKAGE_PREFIX}-mpg123"
         "${MINGW_PACKAGE_PREFIX}-lua"
         "${MINGW_PACKAGE_PREFIX}-libdvdnav"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libcaca"
         "winpty-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python3-docutils"
             "python"
             "perl"
             "pkg-config")
source=(https://github.com/mpv-player/${_realname}/archive/v$pkgver.tar.gz)
md5sums=('b3d02a0db096e77ce38c3946dd631e58')

# strip doesn't work well with the mpv.com wrapper, so strip manually instead
options=(!strip !emptydirs)

prepare() {
  cd ${_realname}-${pkgver}
  [ -x ./waf ] || /usr/bin/python3 ./bootstrap.py
  sed -i 's:bin/env python$:bin/env python3:' waf
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  DEST_OS=win32 \
  TARGET=${MINGW_CHOST} \
  PKG_CONFIG=/usr/bin/pkg-config \
  CC=gcc PERL=/usr/bin/perl \
  AR=${MINGW_PREFIX}/bin/ar \
  WINDRES=${MINGW_PREFIX}/bin/windres \
  RST2MAN=${MINGW_PREFIX}/bin/rst2man3 \
  /usr/bin/python3 ./waf configure \
    --prefix=${MINGW_PREFIX} \
    --check-c-compiler=gcc \
    --enable-libmpv-shared \
    --enable-libguess \
    --enable-lua \
    --enable-libass \
    --enable-libbluray \
    --enable-dvdread \
    --enable-dvdnav \
    --disable-cdda \
    --enable-enca \
    --enable-mpg123 \
    --enable-lcms2 \
    --enable-caca \
    --enable-jpeg \
    --out="${srcdir}/build-${MINGW_CHOST}"

  /usr/bin/python3 ./waf build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  export PYTHON=${MINGW_PREFIX}/bin/python3
  /usr/bin/python3 ./waf install --destdir="${pkgdir}"

  # For some reason waf installs import libraries to /bin, so move to /lib
  mkdir "${pkgdir}${MINGW_PREFIX}/lib"
  mv "${pkgdir}${MINGW_PREFIX}/bin/pkgconfig" "${pkgdir}${MINGW_PREFIX}/lib/"
  mv "${pkgdir}${MINGW_PREFIX}/bin/"*.a "${pkgdir}${MINGW_PREFIX}/lib/"
  sed -i "s:${MINGW_PREFIX}/bin:${MINGW_PREFIX}/lib:" "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/mpv.pc"

  # Strip binaries manually
  ${MINGW_PREFIX}/bin/strip "${STRIP_BINARIES}" "${pkgdir}${MINGW_PREFIX}/bin/mpv.exe"
  ${MINGW_PREFIX}/bin/strip "${STRIP_BINARIES}" "${pkgdir}${MINGW_PREFIX}/bin/mpv.com"
  ${MINGW_PREFIX}/bin/strip "${STRIP_SHARED}" "${pkgdir}${MINGW_PREFIX}/bin/mpv-1.dll"

  # Move encoding-profiles.conf to share/doc alongside the example .conf files.
  # mpv doesn't search /etc for configuration on MinGW.
  mv "${pkgdir}${MINGW_PREFIX}/etc/mpv/"*.conf "${pkgdir}${MINGW_PREFIX}/share/doc/mpv/"

  # mpv needs winpty for key bindings to work on the terminal
  mv "${pkgdir}${MINGW_PREFIX}/bin/mpv.exe" "${pkgdir}${MINGW_PREFIX}/bin/mpv_exe"
  _exename=mpv
  echo '#!/usr/bin/env bash' > "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"
  echo '/usr/bin/winpty "$( dirname ${BASH_SOURCE[0]} )/'${_exename}'.exe" "$@"' >> "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"
  mv "${pkgdir}${MINGW_PREFIX}/bin/mpv_exe" "${pkgdir}${MINGW_PREFIX}/bin/mpv.exe"
}
