# Maintainer: Daniel Espinosa <esodan@gmail.com>

_realname=gxml
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.1
pkgrel=2
arch=('any')
groups=("${MINGW_PACKAGE_PREFIX}")
pkgdesc="LibXML2 GObject wrapper version 0.4 (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-gettext"
        "${MINGW_PACKAGE_PREFIX}-zlib"
        "${MINGW_PACKAGE_PREFIX}-xz"
        "${MINGW_PACKAGE_PREFIX}-libxml2"
        "${MINGW_PACKAGE_PREFIX}-glib2>=2.34.0"
        "${MINGW_PACKAGE_PREFIX}-libgee"
        "${MINGW_PACKAGE_PREFIX}-vala"
        "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "${MINGW_PACKAGE_PREFIX}-libtool")
options=('strip' 'staticlibs')
license=('LGPL')
url="http://live.gnome.org/GXml"
source=("http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver:0:3}/${_realname}-$pkgver.tar.xz")
sha256sums=('f821c5b076208aa311151c263b951aaab27991663edd1918c05522aa0463663d')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  # Get proper unprefixed directories for gobject-introspection files
  autoreconf -fiv
}

build()
{
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-introspection
  make -j1
}

package()
{
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
