# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gss
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r1479.6fdb450
pkgrel=1
pkgdesc="GNU Generic Security Service (mingw-w64)"
arch=('any')
url="http://www.gnu.org/software/gss/"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "git" "wget" "texinfo")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-shishi")
source=("${_realname}"::"git+git://git.sv.gnu.org/gss.git"
        001-autoconf.patch
        002-gnulib.patch)
md5sums=('SKIP'
         'c1992b9d05bf29af92d44b84b5b80189'
         '4984e4ef1bc87635b9d71cd96b8a3737')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  patch -p1 -i ${srcdir}/001-autoconf.patch
  patch -p1 -i ${srcdir}/002-gnulib.patch
  touch doc/Makefile.gdoc
  rm -f aclocal.m4
  cp -rf build-aux/snippet ${srcdir}/
  WANT_AUTOMAKE=latest autoreconf -fi
  cp -rf ${srcdir}/snippet build-aux/
  cd ..

  cp -rf ${_realname} build-${MINGW_CHOST}
}

build() {
  cd build-${MINGW_CHOST}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static
  make update-po
  make -j1
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
