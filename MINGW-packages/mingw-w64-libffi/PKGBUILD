# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=libffi
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.2.1
pkgrel=2
pkgdesc="A portable, high level programming interface to various calling conventions (mingw-w64)"
arch=('any')
groups=("${MINGW_PACKAGE_PREFIX}")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip')
license=('MIT')
url="http://sourceware.org/libffi"
source=("ftp://sourceware.org/pub/libffi/${_realname}-${pkgver}.tar.gz")
md5sums=('83b89587607e3eb65c70d361f13bab43')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
}

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
