# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_realname=clutter-gtk
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.0
pkgrel=1
pkgdesc="Clutter integration with GTK+. (mingw-w64)"
arch=('any')
url="http://clutter-project.org/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             #"git"
            )
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
        "${MINGW_PACKAGE_PREFIX}-clutter"
        )
options=(!libtool strip staticlibs)
source=(http://download.gnome.org/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        #"git://git.gnome.org/$_realname#tag=$pkgver"
        "0001-msys-add-no-undefined-to-libtool-flags.patch"
        "0002-msys-fix-introspection-build.patch"
        "0003-msys-update-clutter-win32-header-location.patch")
md5sums=('08e5d7c249733d4d96bbbd2dc970a06c'
         '317a8e3259f818c2e1147697d623e920'
         '4b892afaf561ac2503962d238770f967'
         '48afee140d22c548a85691d1ca2097a3')

prepare() {
  cd ${_realname}-$pkgver
  patch -p1 -i ${srcdir}/0001-msys-add-no-undefined-to-libtool-flags.patch
  patch -p1 -i ${srcdir}/0002-msys-fix-introspection-build.patch
  patch -p1 -i ${srcdir}/0003-msys-update-clutter-win32-header-location.patch
  autoreconf -fvi
  #NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-$pkgver/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --disable-static \
    --enable-shared \
    --enable-silent-rules \
    --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
}
