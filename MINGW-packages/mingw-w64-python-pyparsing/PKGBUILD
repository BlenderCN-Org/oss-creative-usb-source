# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=pyparsing
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-$_realname" "${MINGW_PACKAGE_PREFIX}-python3-$_realname")
pkgver=2.0.2
pkgrel=1
pkgdesc='General parsing module for Python (mingw-w64)'
arch=('any')
url='http://pyparsing.wikispaces.com'
license=('MIT')
makedepends=(
            "${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-python2"
)
source=("http://downloads.sourceforge.net/pyparsing/pyparsing-$pkgver.tar.gz")
sha256sums=('17e43d6b17588ed5968735575b3983a952133ec4082596d214d7090b56d48a06')

prepare() {
  cd "${srcdir}"
  cp -r pyparsing-$pkgver python2-pyparsing-$pkgver
  cp -r pyparsing-$pkgver python3-pyparsing-$pkgver
}

package_python3-pyparsing() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd $srcdir/python3-pyparsing-$pkgver
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-pyparsing() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd $srcdir/python2-pyparsing-$pkgver
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-pyparsing() {
  package_python2-pyparsing
}

package_mingw-w64-i686-python3-pyparsing() {
  package_python3-pyparsing
}

package_mingw-w64-x86_64-python2-pyparsing() {
  package_python2-pyparsing
}

package_mingw-w64-x86_64-python3-pyparsing() {
  package_python3-pyparsing
}
