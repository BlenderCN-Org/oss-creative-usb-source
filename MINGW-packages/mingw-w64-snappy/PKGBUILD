# Maintainer: Norbert Pfeiler <norbert.pfeiler@gmail.com>

_realname=snappy
pkgname="$MINGW_PACKAGE_PREFIX-${_realname}"
pkgver=1.1.1
pkgrel=1
pkgdesc="A fast C++ compressor/decompressor library (mingw-w64)"
arch=('any')
license=('New BSD License')
url="http://snappy.googlecode.com"
depends=("$MINGW_PACKAGE_PREFIX-gcc-libs")
makedepends=("$MINGW_PACKAGE_PREFIX-gcc"
             "make")
checkdepends=("$MINGW_PACKAGE_PREFIX-zlib"
              "$MINGW_PACKAGE_PREFIX-lzo2")
source=("http://snappy.googlecode.com/files/${_realname}-$pkgver.tar.gz"
        001-no-undefined.patch)
md5sums=('8887e3b7253b22a31f5486bca3cbc1c2'
         '8df88ce30eafb903bb9e51a57b947da7')

prepare() {
  pushd ${_realname}-$pkgver > /dev/null
  patch -p1 -i ${srcdir}/001-no-undefined.patch
  autoreconf -fi
  popd > /dev/null

  mkdir -p build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ../${_realname}-$pkgver/configure \
    --prefix="$MINGW_PREFIX" \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}
}

build() {
  cd "build-${MINGW_CHOST}"
  make
}

check() {
  cd "build-${MINGW_CHOST}"
  ./snappy_unittest.exe
}

package() {
  cd "build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
