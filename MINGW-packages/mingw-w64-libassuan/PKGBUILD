# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libassuan
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.0
pkgrel=1
pkgdesc="A IPC library used by some GnuPG related software (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.gnupg.org/related_software/libassuan"
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-libgpg-error")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=(ftp://ftp.gnupg.org/gcrypt/$_realname/$_realname-$pkgver.tar.bz2{,.sig}
        0001-fix-def-files.mingw.patch
        0002-enable-silent-rules.mingw.patch
        0003-remove-redef.mingw.patch
        0004-enable-gnu.mingw.patch
        0005-better-check-for-stpcpy.mingw.patch)

sha1sums=('7cf0545955ce414044bb99b871d324753dd7b2e5'
          'SKIP'
          '9e1e65633f643a853283caf21adb9b8f9001c38b'
          'a380ea3781f95a3fd30e8d6b469d870f85f0ea29'
          '3a8f898b6adfe5c820d5ed0f90cae1522d5f5c72'
          'b329ac86f3c908a2753ccd896a0c78de7c8eef7a'
          '5a76f6d350b887c88eb49afec21490fe6103e4ac')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-fix-def-files.mingw.patch
  patch -p1 -i ${srcdir}/0002-enable-silent-rules.mingw.patch
  patch -p1 -i ${srcdir}/0003-remove-redef.mingw.patch
  patch -p1 -i ${srcdir}/0004-enable-gnu.mingw.patch
  patch -p1 -i ${srcdir}/0005-better-check-for-stpcpy.mingw.patch

  autoreconf -fi
}

build() {
  if [ $MINGW_CHOST = "x86_64-w64-mingw32" ]; then
    CFLAGS+=" -DHAVE_DOSISH_SYSTEM=1"
  fi
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static
  make
}

check() {
  cd ${srcdir}/build-${CARCH}
  #make check
}

package() {
  cd ${srcdir}/build-${CARCH}
  make DESTDIR=${pkgdir} install
}
