# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libnice
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.1.10
pkgrel=1
pkgdesc="An implementation of the IETF's draft ICE (for p2p UDP data streams) (mingw-w64)"
arch=('any')
url="http://nice.freedesktop.org"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gstreamer"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "gtk-doc")
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
optdepends=("${MINGW_PACKAGE_PREFIX}-gstreamer")
options=('staticlibs' 'strip')
source=("http://nice.freedesktop.org/releases/${_realname}-$pkgver.tar.gz"
        0001-fix-redefinition-mingw-w64.patch
        0002-no-undefined.patch
        0003-give-cc-to-gir-scanner.all.patch
        0004-mingw-w64-types.patch)
md5sums=('27b62d0093ce29a39df1c6fcf0bb4396'
         'b817c8a7c4cbb405341de75b9824204f'
         '5c937f7c3a5857038f27a009d33b0757'
         'aa92989cd75e24fd2ead5a4e6f334cc7'
         '2663a220cc777082096610353d7051f2')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-fix-redefinition-mingw-w64.patch
  patch -p1 -i ${srcdir}/0002-no-undefined.patch
  patch -p1 -i ${srcdir}/0003-give-cc-to-gir-scanner.all.patch
  patch -p1 -i ${srcdir}/0004-mingw-w64-types.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --enable-introspection

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  rm -f ${pkgdir}${MINGW_PREFIX}/lib/gstreamer*/*.a
}
