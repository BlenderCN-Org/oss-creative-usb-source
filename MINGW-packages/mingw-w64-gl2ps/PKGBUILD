# Maintainer: hauptmech # Contributor: figo.zhang, chubtuff, lubosz
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>
#
# Matlab bindings are not built by default to reduce dependencies.

_realname=gl2ps
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3.8
pkgrel=3
pkgdesc="an OpenGL to PostScript printing library (mingw-w64)"
arch=('any')
url='http://geuz.org/gl2ps/'
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake")
optdepends=()
conflicts=()
source=("http://geuz.org/gl2ps/src/gl2ps-${pkgver}.tgz"
        "0001-MSYS2-FHS-layout.patch")
sha1sums=('792e11db0fe7a30a4dc4491af5098b047ec378b1'
          '8aba0e227b70ca013b8405577c42821ee2173533')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}-source
  patch -p1 -i "${srcdir}"/0001-MSYS2-FHS-layout.patch
}

build() {
  cd "${srcdir}"
  [ -d build-${CARCH} ] && rm -rf build-${CARCH}
  mkdir build-${CARCH}
  cd build-${CARCH}

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -G'MSYS Makefiles' \
    ../${_realname}-${pkgver}-source

  make

}

package() {
  cd "${srcdir}/build-${CARCH}"
  make install
}
