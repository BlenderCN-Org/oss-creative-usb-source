# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmaxminddb
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.3
pkgrel=1
pkgdesc="C library for reading MaxMind DB files, including the GeoIP2 databases from MaxMind (mingw-w64)"
arch=('any')
url="http://dev.maxmind.com/geoip/"
license=('Apache-2.0')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-geoip2-database")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libtool"
             "autoconf"
             "git"
             "patch")
options=('!emptydirs')
source=(https://github.com/maxmind/${_realname}/archive/${pkgver}.tar.gz
        "MaxMind-DB"::git+https://github.com/maxmind/MaxMind-DB.git
        libtap::git+https://github.com/zorgnax/libtap.git
        mingw-no-undefined.patch)
sha256sums=('00fcc61463cabd8293a41c44db3ce3a7d9c078db8693a89b7c1e32df4866654e'
            'SKIP'
            'SKIP'
            'f3246d235bd6dfcb6fd8e89fa63de95b0384283fce12a0ebef56e1b2fe9bd248')

prepare() {
  cp -rf ${srcdir}/libtap ${srcdir}/${_realname}-${pkgver}/t/
  cp -rf ${srcdir}/MaxMind-DB ${srcdir}/${_realname}-${pkgver}/t/
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/mingw-no-undefined.patch
  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-$pkgver/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
}
