# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libproxy
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.11
pkgrel=1
pkgdesc="A library that provides automatic proxy configuration management (mingw-w64)"
arch=('any')
url="http://libproxy.googlecode.com"
license=("LGPL")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-python2")
options=('staticlibs' 'strip')
source=("http://libproxy.googlecode.com/files/${_realname}-${pkgver}.tar.gz"
        001-fix-casting-error.patch
        002-fix-win32-close-scoket.patch
        003-fix-leaks.patch
        004-allow-bindings.patch
        005-modules.patch)

md5sums=('3cd1ae2a4abecf44b3f24d6639d2cd84'
         '6c8423fb0b2595d5254c8aac3797a36a'
         'e8f9219bf9e0e6311419cd96a85b4c9d'
         '275ae951b6fcb2608498549c493810d7'
         '35bcd97f6f79d83b0689907f97476e4b'
         '74adbf77b1ba5b99ea11600d9e43c8f9')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  patch -p1 -i ${srcdir}/001-fix-casting-error.patch
  patch -p1 -i ${srcdir}/002-fix-win32-close-scoket.patch
  patch -p1 -i ${srcdir}/003-fix-leaks.patch
  patch -p1 -i ${srcdir}/004-allow-bindings.patch
  patch -p1 -i ${srcdir}/005-modules.patch
}

build() {
  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null

  [[ -d $srcdir/build-${MINGW_CHOST} ]] && rm -rf $srcdir/build-${MINGW_CHOST}
  mkdir $srcdir/build-${MINGW_CHOST}
  cd $srcdir/build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DLIBEXEC_INSTALL_DIR=${pkgdir}${MINGW_PREFIX}/lib/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    -DWITH_PERL=OFF \
    -DWITH_PYTHON=ON \
    ../${_realname}-$pkgver
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  local PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libproxy-1.0.pc
}
