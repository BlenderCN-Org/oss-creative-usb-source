# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=isl

pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.13
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('any')
url="http://freecode.com/projects/isl"
groups=("${MINGW_PACKAGE_PREFIX}")
#depends=("${MINGW_PACKAGE_PREFIX}-gmp")
makedepends=("${MINGW_PACKAGE_PREFIX}-gmp")
options=('staticlibs')
license=('MIT')
source=(http://isl.gforge.inria.fr/${_realname}-${pkgver}.tar.bz2
        #ftp://ftp.linux.student.kuleuven.be/pub/people/skimo/isl/${_realname}-$pkgver.tar.bz2
        isl-0.13-no-undefined.patch)
md5sums=('e4cf20c4c10d5d613457558b1f46b5e2'
         '75cd13d41fb237dbb78daed54c9d50f2')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  patch -p1 -i ${srcdir}/isl-0.13-no-undefined.patch

  autoreconf -fi
}

build() {
  cd "$srcdir/${_realname}-$pkgver"
  ./configure --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --enable-static \
    --disable-shared \
    --with-gmp-prefix=${MINGW_PREFIX}
  make
}

check() {
  cd "$srcdir/${_realname}-$pkgver"
  make check
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
