# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=goocanvas
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.0.2
pkgrel=1
arch=('any')
pkgdesc="Canvas widget for GTK+ that uses the Cairo 2D library (mingw-w64)"
license=("LGPL 2")
url="http://www.gnome.org"
depends=("${MINGW_PACKAGE_PREFIX}-gtk3")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-python2"
            "make"
            "libtool"
            "automake-wrapper"
            "autoconf"
            "gettext"
            "libintl"
            "patch")
options=('strip' '!debug' 'staticlibs')
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver:0:3}/${_realname}-$pkgver.tar.xz
        001-convert-python-path-to-unix.patch)
sha256sums=('f20e5fbef8d1a2633033edbd886dd13146a1b948d1813a9c353a80a29295d1d0'
            '0aabbc30449243076c0533efe54d79b5236e000204d510fdbbcb28d9c452ae5f')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-convert-python-path-to-unix.patch

  autoreconf -fi
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-introspection=no
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
