# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=fribidi

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.19.6
pkgrel=3
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (mingw-w64)"
arch=('any')
license=('LGPL')
url="http://fribidi.org"
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
options=('strip' '!libtool' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
source=("http://fribidi.org/download/${_realname}-${pkgver}.tar.bz2"
        "0001-No-need-to-specialcase-export-symbols-for-OS_WIN32.patch"
        "0003-fix-eols-in-tests-on.mingw.patch")
md5sums=('ce93d862344991173dabb609bf93ca1d'
         'c86fe94d1aa9ee801124b9f393b6d346'
         '98a7907bee0060f3e72e2afa26b71cd9')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-No-need-to-specialcase-export-symbols-for-OS_WIN32.patch
  patch -p1 -i ${srcdir}/0003-fix-eols-in-tests-on.mingw.patch
}

build() {
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ${srcdir}/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --with-glib

  #touch ${srcdir}/${_realname}-${pkgver}/doc/c2man.stamp
  #touch ${srcdir}/${_realname}-${pkgver}/doc/*.3
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
}
