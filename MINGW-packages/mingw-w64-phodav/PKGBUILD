# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=phodav
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4
pkgrel=1
arch=('any')
pkgdesc="A WebDAV server using libsoup (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-libsoup")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-gtk-doc")
options=('strip' 'staticlibs')
license=("GPL 2")
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver}/${_realname}-$pkgver.tar.xz
        001-win.patch)
sha256sums=('5d14bfd4d733f680fca6875586308879b78a3c4892bd95a2d8dc4aabd24614b6'
            'f125c902a291bf0e0bfa1e49732e0724c4d06bfa95b7e0f6e1678cdf34c163ae')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-win.patch

  autoreconf -isf
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST}

  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
