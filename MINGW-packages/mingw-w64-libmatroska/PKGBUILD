# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmatroska
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.4.2
pkgrel=1
pkgdesc="Matroska library (mingw-w64)"
arch=('any')
groups=("${MINGW_PACKAGE_PREFIX}")
license=('LGPL')
url="http://dl.matroska.org/downloads/libebml/"
depends=("${MINGW_PACKAGE_PREFIX}-libebml")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "automake" "autoconf")
options=('staticlibs')
source=("http://dl.matroska.org/downloads/${_realname}/${_realname}-${pkgver}.tar.bz2"
        001-ebml-ldflags.patch)
md5sums=('f8fe42003e072a42a19ed40405f9efdb'
         'f02191d9d009d82d80985e4079eff266')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-ebml-ldflags.patch

  autoreconf -fiv
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install DESTDIR="${pkgdir}"
}
