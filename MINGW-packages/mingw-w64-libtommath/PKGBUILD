# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libtommath
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.42.0
pkgrel=1
pkgdesc="Highly optimized and portable routines for integer based number theoretic applications (mingw-w64)"
url="http://www.libtom.org/"
arch=('any')
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
groups=("${MINGW_PACKAGE_PREFIX}")
options=('staticlibs' 'strip')
source=("http://libtom.org/files/ltm-${pkgver}.tar.bz2"
        'libtommath-0.42.0-makefile.patch')
md5sums=('7380da904b020301be7045cb3a89039b'
         '0bf4d7eaf45e67dfc38c25e39510111a')

prepare() {
  cd $srcdir/${_realname}-$pkgver
  patch -Np1 -i ${srcdir}/libtommath-0.42.0-makefile.patch
}

build() {
  cp -rf ${_realname}-$pkgver build-${MINGW_CHOST}
  cd $srcdir/build-${MINGW_CHOST}

  CFLAGS+=" -I$srcdir/build-${MINGW_CHOST}"
  CC="${MINGW_PREFIX}/bin/gcc" \
  make -f makefile.shared IGNORE_SPEED=1
}

check() {
  cd $srcdir/build-${MINGW_CHOST}

  CC=${MINGW_PREFIX}/bin/gcc \
  make -f makefile.shared mtest
}

package() {
  cd $srcdir/build-${MINGW_CHOST}

  CC="${MINGW_PREFIX}/bin/gcc" \
  make -f makefile.shared \
    DESTDIR=$pkgdir \
    INSTALL_GROUP=root \
    LIBPATH=${MINGW_PREFIX}/lib \
    INCPATH=${MINGW_PREFIX}/include \
    DATAPATH=${MINGW_PREFIX}/share/doc/libtommath/pdf \
    install

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
  chmod 644 ${pkgdir}${MINGW_PREFIX}/lib/libtommath{.dll,}.a
}
