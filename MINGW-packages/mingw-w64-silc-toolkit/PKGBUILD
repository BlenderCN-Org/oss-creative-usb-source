# Maintainer: Renato Silva <br.renatosilva@gmail.com>
# Contributor: David Macek <david.macek.0@gmail.com>

_realname=silc-toolkit
pkgname=(${MINGW_PACKAGE_PREFIX}-${_realname})
pkgver=1.1.12
pkgrel=2
url="http://http://silcnet.org"
pkgdesc="Secure Internet Live Conferencing (mingw-w64)"
license=('GPL2' 'BSD')
arch=('any')
depends=(${MINGW_PACKAGE_PREFIX}-libsystre)
makedepends=(${MINGW_PACKAGE_PREFIX}-gcc)
options=('staticlibs' '!emptydirs')
source=("https://downloads.sourceforge.net/project/silc/silc/toolkit/sources/silc-toolkit-${pkgver}.tar.gz"
        001-remove-outdated-mno-cygwin-gcc-option.patch
        002-fixes-for-64-bit.patch
        003-fix-references-to-slash-usr.patch
        004-link-with-regex-and-silc.patch
        005-fix-some-compiler-check.patch
        006-disable-dllimport-for-silcske.patch)
md5sums=('321536224d4dee2423a3fd05af651eff'
         'f726a755bdd53473cbf8721e7f948714'
         '9df1cb6241c269a194cb4ef6b2bc0504'
         '2bf45e2323f154aa8b56c4465e83f144'
         'd8099a0d6e97bc790d587a4a12f380cd'
         '58509bbd8c6070845e22ebabec3e957a'
         '914cb7a97cd8dcbff35e84090a840536')

prepare() {
  cd "$srcdir/${_realname}-${pkgver}"
  patch -p1 < "${startdir}"/001-remove-outdated-mno-cygwin-gcc-option.patch
  patch -p1 < "${startdir}"/002-fixes-for-64-bit.patch
  patch -p1 < "${startdir}"/003-fix-references-to-slash-usr.patch
  patch -p1 < "${startdir}"/004-link-with-regex-and-silc.patch
  patch -p1 < "${startdir}"/005-fix-some-compiler-check.patch
  patch -p1 < "${startdir}"/006-disable-dllimport-for-silcske.patch
  autoreconf -sif
}

build() {
  cd "$srcdir/${_realname}-${pkgver}"
  ./configure \
      --prefix=${MINGW_PREFIX} \
      --with-simdir=${MINGW_PREFIX}/lib/silc/modules \
      --with-win32
  make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # License
  cd "${pkgdir}${MINGW_PREFIX}/share"
  mkdir -p licenses/${_realname}
  mv -v doc/${_realname}/COPYING licenses/${_realname}/COPYING
  mv -v doc/${_realname}/GPL     licenses/${_realname}/GPL
  mv -v doc/${_realname}/BSD     licenses/${_realname}/BSD
}
