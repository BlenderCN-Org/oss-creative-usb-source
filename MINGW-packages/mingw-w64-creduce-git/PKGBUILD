# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=creduce
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}-git" "${_realname}-git-perl-${CARCH}")
pkgver=creduce.2.2.0.13.gbd8882c
pkgrel=1
arch=('any')
url='https://github.com/csmith-project/creduce'
license=('custom:University of Illinois/NCSA Open Source License')
makedepends=('git' 'flex' "${MINGW_PACKAGE_PREFIX}-clang")
source=('git://github.com/csmith-project/creduce.git'
        "llvm-config-cygpath")
sha256sums=('SKIP'
            '03a7239d1e26d8a5dd5f405e10826679725cae6c01a9c37e80c6f1c184dad0a9')

prepare() {
  cd "$srcdir"
  [ -d tools ] && rm -rf tools
  mkdir tools
  cp -f "$srcdir"/llvm-config-cygpath tools/llvm-config
}

pkgver() {
  cd "$srcdir/$_realname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir"
  [ -d ${CARCH} ] && rm -rf ${CARCH}
  mkdir ${CARCH}
  cd ${CARCH}
  PATH="$srcdir/tools:$PATH" \
    ../$_realname/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST}

  make
}

package_mingw-w64-creduce-git() {
  pkgdesc='A C program reducer (mingw-w64)'
  depends=("${_realname}-git-perl-${CARCH}")
  cd "$srcdir/${CARCH}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "../$_realname/COPYING" "$pkgdir${MINGW_PREFIX}/share/licenses/$_realname/COPYING"

  # Point creduce perl script:
  # lib $FindBin::Bin, '${MINGW_PREFIX}/share/creduce/perl';
  # ..  to msys2 perl.
  sed "s|${MINGW_PREFIX}|/usr|g" \
    -i $pkgdir${MINGW_PREFIX}/bin/creduce
  rm -rf $pkgdir${MINGW_PREFIX}/share/creduce/perl
}

package_creduce-git-perl() {
  pkgdesc='A C program reducer - perl scripts'
  depends=('delta'
#          'astyle'
#          'indent'
           'perl-Benchmark-Timer'
           'perl-Exporter-Lite'
           'perl-File-Which'
           'perl-Getopt-Tabular'
           'perl-Regexp-Common'
           'perl-Sys-CPU'
           )
  cd "$srcdir/${CARCH}"
  make DESTDIR="$pkgdir" install
  # Use sed to switch to MSYS2 perl instead of MinGW-w64 perl.
  rm -rf $pkgdir${MINGW_PREFIX}/{bin,libexec}
  mv $pkgdir${MINGW_PREFIX} $pkgdir/usr
}

package_mingw-w64-x86_64-creduce-git() {
  package_mingw-w64-creduce-git
}

package_mingw-w64-i686-creduce-git() {
  package_mingw-w64-creduce-git
}

package_creduce-git-perl-i686() {
  package_creduce-git-perl
}

package_creduce-git-perl-x86_64() {
  package_creduce-git-perl
}

# configure: WARNING: You must install Artistic Style before running C-Reduce
# configure: WARNING: You must install GNU Indent before running C-Reduce
# configure: WARNING: Read the INSTALL file for info about C-Reduce dependencies
