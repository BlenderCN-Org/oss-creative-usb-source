# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gnome-icon-theme

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.11.5
pkgrel=1
pkgdesc="GNOME icon theme (mingw-w64)"
arch=('any')
url="http://www.gnome.org"
license=("GPL")
makedepends=("intltool")
depends=("${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
    "${MINGW_PACKAGE_PREFIX}-icon-naming-utils"
    "${MINGW_PACKAGE_PREFIX}-gtk2"
    "${MINGW_PACKAGE_PREFIX}-gnome-icon-theme-symbolic"
    )
groups=("${MINGW_PACKAGE_PREFIX}-gnome")
options=(!libtool strip staticlibs)
install=theme-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz)
sha256sums=('345dce06d4071f7c71b2659aa8834f70349a49fe8f91e6f0006606456dfeb230')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib
  mv ${pkgdir}${MINGW_PREFIX}/share/pkgconfig ${pkgdir}${MINGW_PREFIX}/lib/
  mv ${pkgdir}${MINGW_PREFIX}/lib/locale ${pkgdir}${MINGW_PREFIX}/share/
  rm -f  "${pkgdir}${MINGW_PREFIX}/share/icons/gnome/icon-theme.cache"
}
