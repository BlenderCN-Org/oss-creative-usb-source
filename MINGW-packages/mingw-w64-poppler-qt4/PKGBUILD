# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=poppler

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-qt4"
pkgver=0.26.3
pkgrel=1
pkgdesc="PDF rendering library based on xpdf 3.0 (mingw-w64)"
arch=('any')
url="http://poppler.freedesktop.org"
license=("GPL")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-glib2"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            "${MINGW_PACKAGE_PREFIX}-qt4")
depends=("${MINGW_PACKAGE_PREFIX}-icu"
        "${MINGW_PACKAGE_PREFIX}-libtiff"
        "${MINGW_PACKAGE_PREFIX}-curl"
        "${MINGW_PACKAGE_PREFIX}-openjpeg"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        "${MINGW_PACKAGE_PREFIX}-freetype"
        "${MINGW_PACKAGE_PREFIX}-libjpeg"
        "${MINGW_PACKAGE_PREFIX}-cairo"
        "${MINGW_PACKAGE_PREFIX}-zlib"
        "${MINGW_PACKAGE_PREFIX}-poppler-data")
optdepends=("${MINGW_PACKAGE_PREFIX}-glib2: libpoppler-glib"
            "${MINGW_PACKAGE_PREFIX}-qt4: libpoppler-qt4")
options=('strip' 'staticlibs')
source=("http://poppler.freedesktop.org/poppler-${pkgver}.tar.xz"
        "give-cc-to-gir-scanner.mingw.patch"
        "mingw-no-fpic.patch")
md5sums=('e101f6653f9f68ab63dac63ec34801d1'
         '727c1b414987f6ee2ba841d9db57a967'
         'c319527f31989e1eee561f008b649500')

prepare() {
  cd "$srcdir/poppler-$pkgver"
  patch -p1 -i "$srcdir/give-cc-to-gir-scanner.mingw.patch"
  patch -p1 -i "$srcdir/mingw-no-fpic.patch"
  #sed -i -e '/AC_PATH_XTRA/d' configure.ac
  #sed -i "s:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:" configure.ac
  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-xpdf-headers \
    --enable-zlib \
    --enable-libcurl \
    --disable-gtk-test \
    --enable-utils \
    --disable-gtk-doc-html \
    --with-font-configuration=win32
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
}
