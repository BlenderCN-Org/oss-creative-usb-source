# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=headers
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
replaces="${MINGW_PACKAGE_PREFIX}-${_realname}-svn"
pkgdesc="MinGW-w64 headers for Windows"
pkgver=5.0.0.4451.4ef3dd3
pkgrel=1
arch=('any')
url="http://mingw-w64.sourceforge.net"
license=('custom')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain" "${MINGW_PACKAGE_PREFIX}")
makedepends=('git' "${MINGW_PACKAGE_PREFIX}-tools")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
options=('!strip' '!libtool' '!emptydirs')
source=("${_realname}"::"git://git.code.sf.net/p/mingw-w64/mingw-w64"
        "0001-d3d11-Add-prototype-for-CreateDeviceAndSwapChain.patch"
        "0002-dxgi-Add-interface-for-IDXGISurface1.patch"
        "0003-dxgi-Add-missing-dxgi-1.2-structs-and-interfaces.patch"
        "0004-d3d11-Add-missing-d3d11-1.1-structs-and-interfaces.patch"
        "0005-Add-missing-DirectX11-enum.patch")
md5sums=('SKIP'
         '0bcf1726f2093c6ed7e998d7e6c9dd16'
         '20172a91981ffa87aa6aa5d3001c290b'
         '410ff7ad4443a9f2e318c64742b2d358'
         '28a4074ee6fcf76b5b13db2a8cd3291c'
         '652859ad478448a70c2074a854a313ec')

pkgver() {
  cd "$srcdir/$_realname"
  local _major=$(head -n 16 mingw-w64-headers/crt/_mingw_mac.h | grep '__MINGW64_VERSION_MAJOR' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  local _minor=$(head -n 16 mingw-w64-headers/crt/_mingw_mac.h | grep '__MINGW64_VERSION_MINOR' | sed -e 's/.* //' | tr '\n' '.' | sed 's/.$/\n/')
  local _rev=0
  printf "%s.%s.%s.%s.%s" ${_major} ${_minor} ${_rev} "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${_realname}

  git am "${srcdir}/"0001-d3d11-Add-prototype-for-CreateDeviceAndSwapChain.patch
  git am "${srcdir}/"0002-dxgi-Add-interface-for-IDXGISurface1.patch
  git am "${srcdir}/"0003-dxgi-Add-missing-dxgi-1.2-structs-and-interfaces.patch
  git am "${srcdir}/"0004-d3d11-Add-missing-d3d11-1.1-structs-and-interfaces.patch
  git am "${srcdir}/"0005-Add-missing-DirectX11-enum.patch

  cd ${srcdir}/${_realname}/mingw-w64-headers
  touch include/windows.*.h include/wincrypt.h include/prsht.h
}

build() {
  msg "Configuring ${MINGW_CHOST} headers"
  mkdir -p ${srcdir}/headers-${MINGW_CHOST} && cd ${srcdir}/headers-${MINGW_CHOST}
  ${srcdir}/${_realname}/mingw-w64-headers/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX}/${MINGW_CHOST} \
    --enable-sdk=all \
    --enable-secure-api \
    --enable-idl --with-widl
}

package() {
  msg "Installing ${MINGW_CHOST} headers"
  cd ${srcdir}/headers-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  rm ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_signal.h
  rm ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_time.h
  rm ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/pthread_unistd.h

  msg "Installing MinGW-w64 licenses"
  install -Dm644 ${srcdir}/${_realname}/mingw-w64-headers/ddk/readme.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/ddk-readme.txt
  install -Dm644 ${srcdir}/${_realname}/mingw-w64-headers/direct-x/COPYING.LIB ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/direct-x-COPYING.LIB
  install -Dm644 ${srcdir}/${_realname}/mingw-w64-headers/direct-x/readme.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/direct-x-readme.txt
}
