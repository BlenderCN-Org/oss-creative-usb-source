# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gtkspell3
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.0.6
pkgrel=1
pkgdesc="Provides word-processor-style highlighting and replacement of misspelled words in a GtkTextView widget (mingw-w64)"
arch=('any')
url="http://gtkspell.sourceforge.net/"
license=("GPL")
makedepends=("gtk-doc"
            "${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-vala"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            "${MINGW_PACKAGE_PREFIX}-iso-codes"
            "intltool")
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-gtk2"
        "${MINGW_PACKAGE_PREFIX}-enchant")
options=(!libtool strip staticlibs)
source=(http://gtkspell.sourceforge.net/download/${_realname}-${pkgver}.tar.gz
        001-win.patch)
md5sums=('cdc550a06dc424c8c497672bc54649db'
         '761980f7811a41f4dab9a2a797946316')

prepare() {
  cd ${_realname}-$pkgver

  patch -p1 -i ${srcdir}/001-win.patch

  WANT_AUTOMAKE=latest autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --disable-static \
    --enable-gtk2
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
}
