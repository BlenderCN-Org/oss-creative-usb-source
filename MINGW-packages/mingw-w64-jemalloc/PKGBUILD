# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=jemalloc
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.6.0
pkgrel=1
pkgdesc="General-purpose scalable concurrent malloc implementation (mingw64)"
arch=('any')
url="http://www.canonware.com/jemalloc/"
license=('BSD')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=(http://www.canonware.com/download/jemalloc/${_realname}-$pkgver.tar.bz2
        001-fix-library-extension.patch)
sha256sums=('e16c2159dd3c81ca2dc3b5c9ef0d43e1f2f45b04548f42db12e7c12d7bdf84fe'
            '074700fcdb9d0548668d5e1f8a61b3571e35c839f45036146bb2e112ad5a5d9d')

prepare() {
  cd $srcdir/${_realname}-$pkgver
  patch -p1 -i ${srcdir}/001-fix-library-extension.patch

  # ./autogen.sh

}
build() {
  cd $srcdir/${_realname}-$pkgver
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  CFLAGS+=" -std=gnu11"

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR=$pkgdir install

  mv "${pkgdir}${MINGW_PREFIX}"/bin/{,jemalloc-}pprof
  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin/
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/COPYING "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
