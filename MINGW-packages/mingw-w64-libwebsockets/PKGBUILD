# Maintainer: Martell Malone <martell malone at g mail dot com >

_realname=libwebsockets
_srcbase=chrome37-firefox30
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.3
pkgrel=1
arch=('any')
pkgdesc="A lightweight pure C library for websockets (mingw-w64)"
url=('http://libwebsockets.org/')
license=('LGPL 2.1 - custom')
depends=("${MINGW_PACKAGE_PREFIX}-zlib" "${MINGW_PACKAGE_PREFIX}-openssl")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-gcc")
source=("https://github.com/warmcat/${_realname}/archive/v${pkgver}-${_srcbase}.tar.gz"
        fix-cmake-files-location.patch)
options=(!strip !buildflags staticlibs)
md5sums=('63117f35174a5fe4ec062f6baa411638'
         '1223de5b20e776c823f27b62ab352249')

prepare() {
  cd ${_realname}-${pkgver}-${_srcbase}
  patch -p1 -i ${srcdir}/fix-cmake-files-location.patch
}

build() {
  mkdir ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
    ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DLWS_USE_EXTERNAL_ZLIB=ON \
    -DCMAKE_C_COMPILER=${MINGW_PREFIX}/bin/gcc.exe \
    -DCMAKE_CXX_COMPILER=${MINGW_PREFIX}/bin/g++.exe \
    -DLWS_WITHOUT_DAEMONIZE=ON \
    -DLWS_WITHOUT_TESTAPPS=ON \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=RELEASE \
    ../${_realname}-${pkgver}-${_srcbase}
    make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 install
  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin
  install -Dm644 "${srcdir}/${_realname}-${pkgver}-${_srcbase}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
