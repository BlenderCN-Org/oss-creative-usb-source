# Maintainer: Andrew Leech <andrew@alelec.net>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=libusb-compat
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides="${MINGW_PACKAGE_PREFIX}-${_realname}"
conflicts="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=r60.072a5e4
pkgrel=1
pkgdesc="libusb provides generic access to USB devices (mingw-w64)"
arch=('any')
url="http://libusb.info"
depends=(${MINGW_PACKAGE_PREFIX}-libusb)
options=('staticlibs')
makedepends=("git"
             "pkgconfig"
             "m4")
source=(${_realname}::git+https://github.com/libusb/libusb-compat-0.1.git
        0001-Add-some-missing-definitions.patch
        0002-Add-no-undefined-to-LDFLAGS.patch)
md5sums=('SKIP'
         '568ddc63b0e3f24bcbae37367261007c'
         '1446f870923cd037c59a44d203657ca7')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  git am ${srcdir}/0001-Add-some-missing-definitions.patch
  git am ${srcdir}/0002-Add-no-undefined-to-LDFLAGS.patch

  ./bootstrap.sh
}

build() {
  [ -d ${srcdir}/build-${CARCH} ] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH}
  cd ${srcdir}/build-${CARCH}

  local -a myconf
  myconf+=(--prefix=${MINGW_PREFIX}
           --{host,build}=${MINGW_CHOST}
           --disable-dependency-tracking
           --enable-static
           --enable-shared)
  echo "../${_realname}/configure ${myconf[@]}"
  ../${_realname}/configure ${myconf[@]}
  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make DESTDIR="${pkgdir}" install
}
