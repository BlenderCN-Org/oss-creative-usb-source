# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

_realname=gst-python
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.3.0.1.1283.3483187
pkgrel=1
pkgdesc='GStreamer GObject Introspection overrides for Python 3'
arch=('any')
license=('LGPL')
depends=("${MINGW_PACKAGE_PREFIX}-gstreamer"
        "${MINGW_PACKAGE_PREFIX}-python3"
        "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
makedepends=('git')
url='http://gstreamer.freedesktop.org/modules/gst-python.html'
options=(!libtool strip staticlibs)

source=("git://anongit.freedesktop.org/gstreamer/${_realname}"
        '0001-msys2-python3-config-returns-1-so-use-which.patch'
        '0002-msys2-fix-linking-errors.patch')
md5sums=('SKIP'
        'eb52a3ef8270c2d263c79799bcaf377a'
        'a1d96c7bf521664a76d424c1b9250ea8')

pkgver() {
  cd $_realname
  version=$(grep AC_INIT configure.ac | sed 's/AC_INIT(GStreamer GObject Introspection overrides for Python , //' | sed 's/,//')
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  echo $version.$revision.$hash
}

prepare() {
  cd $_realname
  git am ../0001-msys2-python3-config-returns-1-so-use-which.patch
  git am ../0002-msys2-fix-linking-errors.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd $_realname
    PYTHON=${MINGW_PREFIX}/bin/python3 \
      ./autogen.sh --prefix=${MINGW_PREFIX} \
        --build=${MINGW_CHOST} \
        --host=${MINGW_CHOST} \
        --target=${MINGW_CHOST} \
        --disable-silent-rules
    make
}

package() {
    cd $_realname
    make DESTDIR="$pkgdir" install
}

