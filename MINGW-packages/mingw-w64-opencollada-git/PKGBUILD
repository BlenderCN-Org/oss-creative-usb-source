# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=opencollada
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgver=r1027.729147d
pkgrel=1
pkgdesc="Stream based reader and writer library for COLLADA files (mingw-w64)"
arch=('any')
url="https://github.com/khronosGroup/OpenCOLLADA/"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "${MINGW_PACKAGE_PREFIX}-cmake" "git")
depends=("${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-pcre")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
options=('staticlibs' 'strip')
source=("${_realname}"::"git://github.com/KhronosGroup/OpenCOLLADA.git"
        fix-mingw-w64.patch
        fix-missing-include.patch)
md5sums=('SKIP'
         'd7c4b3a0c59eae4351b627d4eb714645'
         '11ff58142f0feb1dca80f004615a01ca')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -p0 -i ${srcdir}/fix-mingw-w64.patch
  patch -p1 -i ${srcdir}/fix-missing-include.patch
}

build() {
  mkdir -p "${srcdir}"/build-${MINGW_CHOST}
  cd "${srcdir}"/build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/cmake.exe \
     -G"MSYS Makefiles" \
     -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
     -DCMAKE_BUILD_TYPE=Release \
     ../${_realname}
  make
}

package() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  make -j1 install
}
