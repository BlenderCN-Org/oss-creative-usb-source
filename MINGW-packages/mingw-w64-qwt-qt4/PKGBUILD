# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: AlexWMF <alexxwmf@gmail.com>

_realname=qwt
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-qt4"
pkgver=6.1.0
pkgrel=1
pkgdesc="Qt Widgets for Technical Applications"
arch=('any')
license=("custom:$pkgname")
url="http://qwt.sourceforge.net/"
depends=("${MINGW_PACKAGE_PREFIX}-qt4")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=(!strip staticlibs !buildflags)
source=("http://downloads.sourceforge.net/${_realname}/${_realname}-$pkgver.tar.bz2" \
        "qwtconfig-msys2.pri")
md5sums=('aef0437b37f191067a6a9dc01c30ba64'
         'b0c93b559ba7aa08284f24f5c38093cb')

prepare() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  cd ${srcdir}/${_realname}-${pkgver}
  # copy our config file to the right place
  cp ${srcdir}/qwtconfig-msys2.pri qwtconfig.pri
  sed -s "s|%VALUE_QWT_INSTALL_PREFIX%|${PREFIX_WIN}|g" \
    -i qwtconfig.pri
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  ${MINGW_PREFIX}/bin/qmake qwt.pro

  make ${MAKEFLAGS} # VERBOSE=1
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make -j1 install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/bin/
  mv "${pkgdir}${MINGW_PREFIX}"/lib/*.dll "${pkgdir}${MINGW_PREFIX}"/bin/

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/mkspecs/features/qwtconfig.pri
}
