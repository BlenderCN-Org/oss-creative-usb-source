# Maintainer: AlexWMF <alexxwmf@gmail.com>

_realname=qca
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-qt4-git"
pkgver=2212.e57f8e7
pkgrel=1
pkgdesc="Qt Cryptographic Architecture"
arch=('any')
url="http://delta.affinix.com/qca/"
license=('LGPL')
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-qt5")
makedepends=("${MINGW_PACKAGE_PREFIX}-qt4"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-openssl"
             "${MINGW_PACKAGE_PREFIX}-gnupg"
             "${MINGW_PACKAGE_PREFIX}-libgcrypt"
             "${MINGW_PACKAGE_PREFIX}-cyrus-sasl"
             "${MINGW_PACKAGE_PREFIX}-nss"
             "git")
depends=("${MINGW_PACKAGE_PREFIX}-qt4")
install=${_realname}-qt4-git-${CARCH}.install
source=("${_realname}"::"git://anongit.kde.org/qca.git"
        "cyrus_sasl_linking.patch"
        "find_nss_cmake.patch")
md5sums=('SKIP'
         '89d537c0b449408685ff48f401239544'
         '2ce45122eba6d2518f773bc4c5934e32')

pkgver() {
  cd ${srcdir}/${_realname}
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  patch -p1 -i ${srcdir}/cyrus_sasl_linking.patch
  patch -p1 -i ${srcdir}/find_nss_cmake.patch
}

build() {
  cd ${srcdir}
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}

  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  QC_CERTSTORE_PATH=${MINGW_PREFIX}/etc/ssl/certs/ca-certificates.crt ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=False \
    ${srcdir}/${_realname}

  make ${MAKEFLAGS}
}

package() {
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_DEP=`pwd -W`
  popd > /dev/null

  cd ${srcdir}/build-${MINGW_CHOST}
  make INSTALL_ROOT="${pkgdir}" install

  sed -s "s|${PREFIX_DEP}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/mkspecs/features/crypto.prf
  for f in ${pkgdir}${MINGW_PREFIX}/lib/cmake/Qca/*.cmake; do
    sed -s "s|${PREFIX_DEP}|${MINGW_PREFIX}|g" -i ${f}
  done
}
