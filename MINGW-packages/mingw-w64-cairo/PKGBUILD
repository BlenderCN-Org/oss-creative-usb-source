# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=cairo
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.14.0
pkgrel=3
pkgdesc="Cairo vector graphics library (mingw-w64)"
arch=('any')
url="http://cairographics.org"
license=(LGPL2.1 MPL1.1)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-ghostscript"
             "${MINGW_PACKAGE_PREFIX}-glib2"
             "${MINGW_PACKAGE_PREFIX}-gtk2"
             "${MINGW_PACKAGE_PREFIX}-librsvg"
             "${MINGW_PACKAGE_PREFIX}-libspectre"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-poppler"
            )
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-lzo2"
         "${MINGW_PACKAGE_PREFIX}-pixman"
         "${MINGW_PACKAGE_PREFIX}-zlib"
        )
optdepends=("${MINGW_PACKAGE_PREFIX}-glib2: libcairo-gobject")
options=('strip' 'staticlibs')
source=("http://cairographics.org/releases/cairo-${pkgver}.tar.xz"
        0002-fix-using-strings.patch
        0009-standalone-headers.mingw.patch
        0013-mozilla-transparent-windows.all.patch
        0022-dont-fail-argb-similar.all.patch
        0026-create-argb-fonts.all.patch
        0027-Bug-53121-hack-around.all.patch
        fix-crash-in-fill-xrgb32-lerp-opaque-spans.patch)
sha1sums=('53cf589b983412ea7f78feee2e1ba9cea6e3ebae'
          'eed29a424e5e0bc9bd8d651b6237e541273c3f93'
          '58c548d2791ba20dd7f6e328ff596f746df3aa10'
          'b4b8c6f4931814e7e77ddeb98fecaf70cb807e04'
          'fa601d6d2b2c75b0b1608ab3fa9c2e5b6cc31c9e'
          '9c0e533614782a41af2c3806a43ab7fe9d6a5431'
          'b74c9998d960a64fbf606ebe0284187add0da55a'
          '00b79ab41635a4f42311721bebbb9b3d49f09f46')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0002-fix-using-strings.patch
  patch -p1 -i ${srcdir}/0009-standalone-headers.mingw.patch
  patch -p1 -i ${srcdir}/0013-mozilla-transparent-windows.all.patch
  patch -p1 -i ${srcdir}/0022-dont-fail-argb-similar.all.patch
  patch -p1 -i ${srcdir}/0026-create-argb-fonts.all.patch
  patch -p1 -i ${srcdir}/0027-Bug-53121-hack-around.all.patch
  patch -p1 -i ${srcdir}/fix-crash-in-fill-xrgb32-lerp-opaque-spans.patch

  autoreconf -fi
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  CFLAGS+=" -Wno-implicit-function-declaration"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-win32 \
    --enable-win32-font \
    --enable-png \
    --enable-shared \
    --enable-static \
    --enable-gobject \
    --enable-tee \
    --disable-xlib \
    --disable-xcb \
    --enable-fc \
    --enable-ft \
    --disable-silent-rules \
    ac_cv_prog_GS=${MINGW_PREFIX}/bin/gsc

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install

  # Licenses
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING"          "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING-LGPL-2.1" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING-LGPL-2.1"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING-MPL-1.1"  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING-MPL-1.1"
}
