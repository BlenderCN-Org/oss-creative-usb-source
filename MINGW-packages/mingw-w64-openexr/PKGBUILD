# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openexr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.0
pkgrel=1
pkgdesc="Openexr library for EXR images (mingw-w64)"
arch=('any')
url="http://http://www.openexr.com/"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-ilmbase")
options=('staticlibs' 'strip')
source=("http://download.savannah.nongnu.org/releases/openexr/${_realname}-${pkgver}.tar.gz"
        mingw-w64-fix.patch
        openexr-2.1.0-headers.patch
        openexr-2.1.0_bb44ExpLogTable.patch
        openexr-2.1.0_aligned-malloc.patch
        openexr-2.1.0_cast.patch
        openexr_obsolete-macros.patch
        cmake-soversion.patch
        pull93.patch
        change-suffixes-for-64bit-literals.patch
        win-dwalookups.patch)
md5sums=('b64e931c82aa3790329c21418373db4e'
         '15d924b75a3494c7c722dd0b2e969437'
         'd87df3d057dd8d902c61a3b88f05fd0d'
         '4798df032716e33a2b227542319fa8f5'
         '09df5525cf2d53d013304dd5e533ce0f'
         '4ec9ab6cc823dc9fcc28cfc85a761546'
         'cf78f6e75c212458c77e7e676a77536d'
         '9371e6ceb96e11cc2bd980cfd954fd6d'
         'eebc359f6766a00de4e2f35ef00ac860'
         'c190b400bfe038e70f26e7a86e2219bd'
         'e913e6fa443d2ae28951ed22ae680653')

prepare(){
  cd "$srcdir/${_realname}-$pkgver"

  sed -i 's/#define ZLIB_WINAPI/\/\/#define ZLIB_WINAPI/g' IlmImf/ImfZipCompressor.cpp
  sed -i 's/#define ZLIB_WINAPI/\/\/#define ZLIB_WINAPI/g' IlmImf/ImfPxr24Compressor.cpp

  patch -Np1 -i "$srcdir/mingw-w64-fix.patch"
  patch -Np1 -i "$srcdir/openexr-2.1.0-headers.patch"
  #patch -Np1 -i "$srcdir/openexr-2.1.0_bb44ExpLogTable.patch"
  patch -Np1 -i "$srcdir/openexr-2.1.0_aligned-malloc.patch"
  patch -Np1 -i "$srcdir/openexr-2.1.0_cast.patch"
  patch -Np1 -i "$srcdir/openexr_obsolete-macros.patch"
  patch -Np1 -i "$srcdir/cmake-soversion.patch"
  patch -Np2 -i "$srcdir/pull93.patch"
  patch -p2 -i ${srcdir}/change-suffixes-for-64bit-literals.patch
  patch -p1 -i ${srcdir}/win-dwalookups.patch

}

build() {
  CXXFLAGS+=" -I${MINGW_PREFIX}/include/OpenEXR"
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=ON \
    -DNAMESPACE_VERSIONING=ON \
    -DUSE_ZLIB_WINAPI=OFF \
    ../${_realname}-${pkgver}

  #-DCMAKE_VERBOSE_MAKEFILE=ON
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/OpenEXR.pc
}
