# Maintainer: Patrick von Reth <vonreth@kde.org>

_realname=Snorenotify
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.5.2
pkgrel=1
pkgdesc="Snorenotify is a multi platform Qt notification framework. Using a plugin system it is possible to create notifications with many different notification systems on Windows, Mac OS and Unix. (mingw-w64)"
arch=('any')
license=("LGPL3")
url="https://github.com/Snorenotify/Snorenotify"
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
        "${MINGW_PACKAGE_PREFIX}-SnoreGrowl")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-cmake"
            "${MINGW_PACKAGE_PREFIX}-extra-cmake-modules")
options=(!strip staticlibs !buildflags)
source=("${_realname}-$pkgver".tar.gz::"https://github.com/${_realname}/${_realname}/archive/v${pkgver}.tar.gz")
md5sums=('508dafd77c2cb1b57239c1dc6ad73727')

build() {
  mkdir $srcdir/build-${MINGW_CHOST}
  cd $srcdir/build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_QT4=OFF \
    ${srcdir}/${_realname}-${pkgver}

  make # VERBOSE=1
}

package() {
  cd $srcdir/build-${MINGW_CHOST}
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  for f in ${pkgdir}${MINGW_PREFIX}/lib/cmake/libsnoreQt5/*.cmake; do
    sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${f}
  done
}
