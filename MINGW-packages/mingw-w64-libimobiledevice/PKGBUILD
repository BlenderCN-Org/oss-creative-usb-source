# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=libimobiledevice
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.1.7
pkgrel=2
pkgdesc='A cross-platform protocol library to communicate with iOS devices (mingw-w64)'
groups=("${MINGW_PACKAGE_PREFIX}" "xdev")
arch=('any')
url='http://www.libimobiledevice.org/'
license=('GPL2+')
options=('staticlibs')
source=("http://www.libimobiledevice.org/downloads/${_realname}-${pkgver}.tar.bz2"
        001-cython-includes.patch)
depends=("${MINGW_PACKAGE_PREFIX}-libusbmuxd"
         "${MINGW_PACKAGE_PREFIX}-libplist"
         "${MINGW_PACKAGE_PREFIX}-openssl")
sha1sums=('ac47ba39e7f8d8cb9379756773ece30458b93c80'
          '9d091cf73ce1477db7b78fc8e47572e2e3031e79')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-cython-includes.patch

  autoreconf -fiv
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  PYTHON=${MINGW_PREFIX}/bin/python3 \
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-static \
    --enable-shared \
    --enable-dev-tools

  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  make check
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"

  rm -f ${pkgdir}${MINGW_PREFIX}/lib/python*/site-packages/*.a
}
