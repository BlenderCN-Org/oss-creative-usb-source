# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ilmbase
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.2.0
pkgrel=1
pkgdesc="Base libraries from ILM for OpenEXR (mingw-w64)"
arch=('any')
url="http://http://www.openexr.com/"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "libtool")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=("http://download.savannah.nongnu.org/releases/openexr/${_realname}-${pkgver}.tar.gz"
        ilmthread-mingw-win32.patch
        ilmthread-mingw-pthreads.patch
        ilmbase-2.1.0_obsolete-macros.patch
        cmake-soversion.patch
        cmake-install-binaries.patch
        pull93.patch)
md5sums=('b540db502c5fa42078249f43d18a4652'
         '3a34cf48b52c2af0584ef3e8cf50499a'
         '8b9d9c6e4ed88dc9dfb80ce4f637460a'
         'eeb3115d9db9531ec0f1292b7ef1b386'
         'dd481c883672600aa2f95111b16402ef'
         'baaa4fa9467969a611c70e62efa1fe6b'
         '20447d0f67bbcd7b98d51fd0c37dabec')
prepare(){
  cd "$srcdir/${_realname}-$pkgver"
  # Use either one or the other of the ilmthread patches
  # depending on the sort of threads that you want.
  # patch -Np1 -i "$srcdir/ilmthread-mingw-win32.patch"
  patch -Np1 -i "$srcdir/ilmthread-mingw-pthreads.patch"
  patch -Np1 -i "$srcdir/ilmbase-2.1.0_obsolete-macros.patch"
  patch -Np1 -i "$srcdir/cmake-soversion.patch"
  patch -Np1 -i "$srcdir/cmake-install-binaries.patch"
  patch -Np2 -i "$srcdir/pull93.patch"
  #./bootstrap
}

build() {
  mkdir build-${CARCH}
  cd build-${CARCH}
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DBUILD_SHARED_LIBS=ON \
    -DNAMESPACE_VERSIONING=ON \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/IlmBase.pc
}
