# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: AlexWMF <alexxwmf@gmail.com>

_realname=libtorrent-rasterbar
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.16.17
pkgrel=1
pkgdesc="libtorrent is a feature complete C++ bittorrent implementation focusing on efficiency and scalability."
arch=('any')
license=('BSD')
url="http://www.rasterbar.com/products/libtorrent"
depends=("${MINGW_PACKAGE_PREFIX}-boost"
        "${MINGW_PACKAGE_PREFIX}-openssl")

makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-cmake"
            "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=(staticlibs strip)

source=("http://downloads.sourceforge.net/sourceforge/libtorrent/${_realname}-${pkgver}.tar.gz"
        cmake_pkg_config.patch)
md5sums=('34f01b9f755d1822cb761f8d8eb07bb8'
         '49a025de76b8f525de6a2d2c58d53b43')

prepare() {
  cd "$srcdir/$_realname-$pkgver"
  patch -p0 -i ${srcdir}/cmake_pkg_config.patch
}

build() {
  mkdir $srcdir/build-${MINGW_CHOST}
  cd $srcdir/build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -Dbuild_tests=False \
    -Dbuild_examples=False \
    ${srcdir}/${_realname}-${pkgver}

  make
}

package() {
  cd $srcdir/build-${MINGW_CHOST}
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libtorrent-rasterbar.pc
}
