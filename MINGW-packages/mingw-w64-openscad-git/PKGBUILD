# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openscad
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=r4930.0bf2d4e
pkgrel=1
pkgdesc="The programmers solid 3D CAD modeller (mingw-w64)"
arch=('any')
license=("GPL2")
url="http://openscad.org/"
depends=("${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-cgal"
         "${MINGW_PACKAGE_PREFIX}-opencsg"
         "${MINGW_PACKAGE_PREFIX}-qscintilla"
         "${MINGW_PACKAGE_PREFIX}-shared-mime-info")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-eigen3"
             "git")
options=(!strip staticlibs !buildflags)
source=("${_realname}"::"git+https://github.com/openscad/openscad.git"
        #01-glew-library.patch
        mingw-detect-libs.patch)
md5sums=('SKIP'
         'c4f4af8df6cfbc0b444cbf232f26a958')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_realname}"
  rm -f libraries/MCAD/*.py

  #patch -p1 -i ${srcdir}/01-glew-library.patch
  #patch -p1 -i ${srcdir}/mingw-detect-libs.patch
}

build() {
  cd ${srcdir}/${_realname}
  #BOOSTDIR=${MINGW_PREFIX} \
  #CGALDIR=${MINGW_PREFIX} \
  #EIGENDIR=${MINGW_PREFIX} \
  #GLEWDIR=${MINGW_PREFIX} \
  #OPENCSGDIR=${MINGW_PREFIX} \
  #OPENSCAD_LIBRARIES=${MINGW_PREFIX} \

  ${MINGW_PREFIX}/bin/qmake \
    CONFIG+=release \
    QMAKE_CXXFLAGS+="-O1 -Wa,-mbig-obj" \
    QMAKE_CFLAGS+="-O1 -Wa,-mbig-obj" \
    PREFIX="${pkgdir}${MINGW_PREFIX}"

  make -j1
}

package() {
  cd ${srcdir}/${_realname}
  make install
  install -Dm644 "icons/${_realname}.xml" "${pkgdir}${MINGW_PREFIX}/share/mime/packages/${_realname}.xml"
}
