# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=vala
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.27.1
pkgrel=1
pkgdesc="Compiler for the GObject type system (mingw-w64)"
arch=('any')
url="http://live.gnome.org/Vala"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-libxslt")
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        001-change-pkg-config-invocations.mingw.patch)
sha256sums=('0bce939c011c34478da840f869b3c24d02e8f1c92691c587c1fe289a5533cd77'
            'c588a3a69097aae30ada1d543001d5029865b1dd1f46132d9e60d12e1833b325')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-change-pkg-config-invocations.mingw.patch
  autoreconf -ivf
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
}
