# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=adwaita-icon-theme
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.14.0
pkgrel=1
pkgdesc="GNOME icon theme (mingw-w64)"
arch=('any')
url="http://www.gnome.org"
license=("GPL")
makedepends=("intltool")
depends=("${MINGW_PACKAGE_PREFIX}-hicolor-icon-theme"
        "${MINGW_PACKAGE_PREFIX}-icon-naming-utils"
        "${MINGW_PACKAGE_PREFIX}-gtk2"
        "${MINGW_PACKAGE_PREFIX}-librsvg"
        )
groups=("${MINGW_PACKAGE_PREFIX}-gnome")
options=(!libtool strip staticlibs)
install=theme-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        0001-localedir-fix.patch)
sha256sums=('afacb6025a18be007e33a9663b92cee9d2745279bb00107a68e09b2806f9541f'
            '35b66334d0856bc44ef202c8df1bfa11f0be658b31b946ca49afe1c357c61adb')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-localedir-fix.patch
  autoreconf -fvi
}

build() {
  [ -d ${srcdir}/build-${MINGW_CHOST} ] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST}
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib
  mv ${pkgdir}${MINGW_PREFIX}/share/pkgconfig ${pkgdir}${MINGW_PREFIX}/lib/
}
