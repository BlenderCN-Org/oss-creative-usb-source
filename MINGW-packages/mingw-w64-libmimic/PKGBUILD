# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmimic

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.0.4
pkgrel=1
pkgdesc="An open source video encoding/decoding library for Mimic V2.x (mingw-w64)"
arch=('any')
url="ttp://farsight.sourceforge.net"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-glib2")
options=('strip' 'staticlibs')
source=("http://downloads.sourceforge.net/sourceforge/farsight/${_realname}-${pkgver}.tar.gz"
        0001-update-ac-and-silent-rules.mingw.patch
        0002-no-undefined.mingw.patch
        0003-link-to-glib.mingw.patch)
md5sums=('94f0dbb1d3c253201553a4069555fb84'
         'beabec85654f62716bb433f366167a41'
         'b51df1dac975a779a7a457451fb5872f'
         'e7eedf26c42f266010ef18d4498e2c33')

prepare() {
  cd "$srcdir/${_realname}-${pkgver}"
  patch -p1 -i "$srcdir"/0001-update-ac-and-silent-rules.mingw.patch
  patch -p1 -i "$srcdir"/0002-no-undefined.mingw.patch
  patch -p1 -i "$srcdir"/0003-link-to-glib.mingw.patch

  WANT_AUTOMAKE=latest autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
}
