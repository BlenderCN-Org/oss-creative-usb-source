# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ldns
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.17
pkgrel=2
pkgdesc="Fast DNS library supporting recent RFCs (mingw-w64)"
arch=('any')
url='http://www.nlnetlabs.nl/projects/ldns/'
license=('custom:BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-dnssec-anchors")
options=(strip staticlibs)
source=("http://www.nlnetlabs.nl/downloads/${_realname}/${_realname}-${pkgver}.tar.gz"
        ldns-1.6.17-relocate.patch)
sha1sums=('4218897b3c002aadfc7280b3f40cda829e05c9a4'
          'c2ec4d4f4c909a41a16ef868dba4e3d7a5c146ed')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/ldns-1.6.17-relocate.patch

  WANT_AUTOMAKE=latest autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  export PYTHON=${MINGW_PREFIX}/bin/python2
  cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --sysconfdir=${MINGW_PREFIX}/etc \
    --localstatedir=${MINGW_PREFIX}/var \
    --with-trust-anchor=${MINGW_PREFIX}/etc/trusted-key.key \
    --with-drill \
    --with-examples \
    --without-pyldns \
    --without-pyldnsx

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 ${srcdir}/${_realname}-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
