# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libotr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.0.0
pkgrel=2
pkgdesc="Off-the-Record Messaging Library and Toolkit (mingw-w64)"
arch=('any')
url="http://www.cypherpunks.ca/otr/"
license=('GPL')
depends=("${MINGW_PACKAGE_PREFIX}-libgcrypt")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs' 'strip')
source=("http://www.cypherpunks.ca/otr/${_realname}-${pkgver}.tar.gz"
        '001-kopete.patch'
        '002-libotr-no-undefined.patch'
        '003-fix-symlinks.patch')
md5sums=('00979dca82d70383fcd1b01f3974363c'
         '6ef0928ce2946bd039c89d59fa28707a'
         '83a5e2e8cd03aab67143826025469a1d'
         '4bedcda46f566ccece54cd68585b04f8')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-kopete.patch
  patch -p1 -i ${srcdir}/002-libotr-no-undefined.patch
  patch -p1 -i ${srcdir}/003-fix-symlinks.patch

  autoreconf -fi
}

build() {
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}
  ${srcdir}/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --mandir=${MINGW_PREFIX}/share/man
  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 DESTDIR="$pkgdir" install
}
