# Maintainer: Patrick von Reth <vonreth@kde.org>

_realname=SnoreGrowl
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.4.0
pkgrel=1
pkgdesc="A C and C++ library providing an api to use Growl notifications, based on the GNTP protocol. (mingw-w64)"
arch=('any')
license=("BSD")
url="https://github.com/Snorenotify/Snorenotify"
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-cmake")
options=(!strip staticlibs !buildflags)
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/Snorenotify/${_realname}/archive/v${pkgver}.tar.gz")

build() {
  mkdir $srcdir/build-${MINGW_CHOST}
  cd $srcdir/build-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ${srcdir}/${_realname}-${pkgver}

  make # VERBOSE=1
}

package() {
  cd $srcdir/build-${MINGW_CHOST}
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  for f in ${pkgdir}${MINGW_PREFIX}/lib/cmake/${_realname}/*.cmake; do
    sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" -i ${f}
  done
}
md5sums=('15b76b9e7626706b69574b6ec7928d6c')
