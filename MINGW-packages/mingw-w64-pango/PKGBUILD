# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=pango
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.36.8
pkgrel=2
pkgdesc="A library for layout and rendering of text (mingw-w64)"
arch=('any')
url="http://www.pango.org"

# TODO: what is the license for the package as a whole? The README file says
# that most of the code is LGPL but some parts are "GPL/FreeType". However this
# non-LGPL note makes reference to non-existing files, so would LGPL apply to
# the whole code? If this note is still correct, are the GPL/FreeType parts
# under either or both of these licenses, and which versions? What license is
# compatible with the answers?
license=(partial:'LGPL2')

makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-freetype"
        "${MINGW_PACKAGE_PREFIX}-fontconfig"
        "${MINGW_PACKAGE_PREFIX}-cairo"
        "${MINGW_PACKAGE_PREFIX}-glib2"
        "${MINGW_PACKAGE_PREFIX}-harfbuzz")
options=('staticlibs' 'strip' 'emptydirs')
install=${_realname}-${CARCH}.install
source=("http://ftp.gnome.org/pub/gnome/sources/pango/${pkgver:0:4}/pango-${pkgver}.tar.xz"
        0001-no-unconditional-xft-please.all.patch
        0002-msvc-is-impotent-but-not.mingw.patch)
sha256sums=('18dbb51b8ae12bae0ab7a958e7cf3317c9acfc8a1e1103ec2f147164a0fc2d07'
            'a673ce807b29abe4b0fedf5b17e154986d675cec3e001c4235067b01515c2e4d'
            '240d21474157c0deabc2660593ac0414565ddf30c304b08b74a09072c910b34d')

prepare() {
  cd "$srcdir/pango-$pkgver"
  patch -p1 -i ${srcdir}/0001-no-unconditional-xft-please.all.patch
  patch -p1 -i ${srcdir}/0002-msvc-is-impotent-but-not.mingw.patch

  autoreconf -fi
  sed -i 's/have_libthai=true/have_libthai=false/' configure
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --with-included-modules=yes \
    --with-dynamic-modules=no \
    --enable-introspection \
    --disable-debug \
    --enable-shared \
    --enable-static
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
  install -m755 -d "${pkgdir}${MINGW_PREFIX}/etc/pango"
  find "${pkgdir}${MINGW_PREFIX}" -name '*.def' -o -name '*.exp' | xargs -rtl1 rm

  # License
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/README"  "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/README"
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
