# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cegui
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.8.4
pkgrel=1
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines (mingw-w64)"
arch=('any')
url="http://cegui.org.uk"
license=("MIT")
#makedepends=('doxygen' 'gtk2' 'graphviz' 'irrlicht' 'glm' 'tolua++')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
    "${MINGW_PACKAGE_PREFIX}-pkg-config"
    "${MINGW_PACKAGE_PREFIX}-cmake"
    "${MINGW_PACKAGE_PREFIX}-python2"
    "mercurial")
depends=("${MINGW_PACKAGE_PREFIX}-tinyxml"
    "${MINGW_PACKAGE_PREFIX}-boost"
    "${MINGW_PACKAGE_PREFIX}-DevIL"
    "${MINGW_PACKAGE_PREFIX}-expat"
    "${MINGW_PACKAGE_PREFIX}-FreeImage"
    "${MINGW_PACKAGE_PREFIX}-freetype"
    "${MINGW_PACKAGE_PREFIX}-fribidi"
    "${MINGW_PACKAGE_PREFIX}-glew"
    #"${MINGW_PACKAGE_PREFIX}-glfw"
    "${MINGW_PACKAGE_PREFIX}-glm"
    "${MINGW_PACKAGE_PREFIX}-irrlicht"
    "${MINGW_PACKAGE_PREFIX}-libxml2"
    "${MINGW_PACKAGE_PREFIX}-libiconv"
    "${MINGW_PACKAGE_PREFIX}-lua51"
    "${MINGW_PACKAGE_PREFIX}-ogre3d"
    "${MINGW_PACKAGE_PREFIX}-ois"
    "${MINGW_PACKAGE_PREFIX}-pcre"
    #"${MINGW_PACKAGE_PREFIX}-tolua"
    "${MINGW_PACKAGE_PREFIX}-xerces-c"
    "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("http://downloads.sourceforge.net/crayzedsgui/cegui-${pkgver}.tar.bz2"
    freetype.patch
    mingw-w64-fix.patch
    fix-finding-glew.patch
    fix-finding-directx.patch
    fix-finding-glfw3.patch
    fix-dxerr-libname.patch)
md5sums=('344917301b5ed867a07575ee6a828667'
         '4c5f81f109f36718045bb4ba5ba25602'
         'ac605f26c068cf0b90bc24add094ca6d'
         'b60459d7cf5854dee5b8bbdd519778ea'
         'db4b135df45db6716c242042a8003893'
         '3a754a1caf9ab0cb0c732bd68f6f29a0'
         '1b75c82065e628a7b9afc4367d14c3fa')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  #patch -p1 -i ${srcdir}/freetype.patch
  #patch -p1 -i ${srcdir}/mingw-w64-fix.patch
  patch -p1 -i ${srcdir}/fix-finding-glew.patch
  patch -p1 -i ${srcdir}/fix-finding-directx.patch
  #patch -p1 -i ${srcdir}/fix-finding-glfw3.patch
  patch -p1 -i ${srcdir}/fix-dxerr-libname.patch
}

build() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null

  mkdir $srcdir/build-${MINGW_CHOST}
  cd "$srcdir/build-${MINGW_CHOST}"

  export DXSDK_DIR=${PREFIX_DEPS}/${MINGW_CHOST}
  export CFLAGS="-I${PREFIX_DEPS}/include"
  export CXXFLAGS="-I${PREFIX_DEPS}/include"
  #cd "${srcdir}/${_realname}-${pkgver}"
  ${MINGW_PREFIX}/bin/cmake.exe \
    ../${_realname}-${pkgver} \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${PREFIX_WIN} \
    -DCEGUI_LIB_INSTALL_DIR=lib \
    -DCEGUI_BUILD_PYTHON_MODULES=OFF # currently broken :(
    # -PYTHON_EXECUTABLE=/usr/bin/python2
  make
  #make html
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make -j1 install
}
