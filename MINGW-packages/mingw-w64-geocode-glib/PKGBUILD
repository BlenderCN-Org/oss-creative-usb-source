# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=geocode-glib
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.14.0
pkgrel=1
arch=('any')
pkgdesc="geocoding and reverse geocoding GLib library using Yahoo! Place Finder (mingw-w64)"
license=("LGPL 2")
url="http://www.gnome.org/"
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-json-glib"
         "${MINGW_PACKAGE_PREFIX}-libsoup")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            "${MINGW_PACKAGE_PREFIX}-gtk-doc"
            "make"
            "libtool"
            "automake-wrapper"
            "autoconf"
            "gettext"
            "patch")
options=('strip' '!debug' 'staticlibs')
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver:0:4}/${_realname}-$pkgver.tar.xz
        001-locale.patch
        002-symbols.patch
        003-give-cc-to-gir-scanner.all.patch)
sha256sums=('a19b21a92b8cbfa29a5ae6616c2fdca8567e97480f7bb5c955905f6ae1c72010'
            '3e694f9b07a989cfbd362a4c117cad8d873cbb202b912ecaa2ee409bfbb5168b'
            'e7ddcaf273910b35b60991bf1db1d9dcff38bad09123dd43033cddcfbee224b2'
            '946437e815fc17ddcb4f4c7b52dd80bd1460363e37d211d97ac7ba5e32f49e9a')

prepare() {
  cd ${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-locale.patch
  patch -p1 -i ${srcdir}/002-symbols.patch
  patch -p1 -i ${srcdir}/003-give-cc-to-gir-scanner.all.patch

  autoreconf -fi
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --enable-introspection
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
