# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openshadinglanguage
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
_ver_base=1.5
pkgver=1.5.1381.52ba3ae
pkgrel=2
pkgdesc="Advanced shading language for production GI renderers (mingw-w64)"
arch=('any')
url="https://github.com/imageworks/OpenShadingLanguage/"
license=("custom")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        "${MINGW_PACKAGE_PREFIX}-glew"
        "${MINGW_PACKAGE_PREFIX}-libtiff"
        "${MINGW_PACKAGE_PREFIX}-openimageio"
        "${MINGW_PACKAGE_PREFIX}-freetype"
        "${MINGW_PACKAGE_PREFIX}-intel-tbb"
        "${MINGW_PACKAGE_PREFIX}-ilmbase"
        "${MINGW_PACKAGE_PREFIX}-openexr"
        )
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-clang"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "git")
source=("${_realname}"::"git+https://github.com/imageworks/OpenShadingLanguage.git"
        0001-mingw-w64-fix.patch
        0002-dont-add-flex-include-dir.patch
        0003-fix-OIIO-windows.patch
        0005-Fix-cast-from-void-ptr.patch
        0006-Fix-weird-array-out-of-bounds-error.patch
        0007-Fix-bad-casts-with-intptr_t.patch
        0008-Install-dlls.patch
        0009-llvm-35.patch
        0010-fix-compare-ints.patch)
md5sums=('SKIP'
         'c225e66cd0a59a674b331b26374ba082'
         '78526dd22cea1d42238609a9cdfef816'
         '9c73b150c2696024a44397ef94584463'
         '73851a8ec414f099fe1195ea18d756f9'
         '33d03ef6eb6433c0df111098a6c7b177'
         '85ed91acdd3bd974a18b8d1c9ae3e79b'
         '7816fd1cd6d2fcbb2547384aa14c7760'
         '9c996fd52bef0ecd5b34db1c930b5173'
         '662f4003eade3afd4e00ea3dd39d4d21')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/$_realname
  patch -p1 -i ${srcdir}/0001-mingw-w64-fix.patch
  patch -p1 -i ${srcdir}/0002-dont-add-flex-include-dir.patch
  patch -p1 -i ${srcdir}/0003-fix-OIIO-windows.patch
  patch -p1 -i ${srcdir}/0005-Fix-cast-from-void-ptr.patch
  patch -p1 -i ${srcdir}/0006-Fix-weird-array-out-of-bounds-error.patch
  patch -p1 -i ${srcdir}/0007-Fix-bad-casts-with-intptr_t.patch
  patch -p1 -i ${srcdir}/0008-Install-dlls.patch
  #patch -p1 -i ${srcdir}/0009-llvm-35.patch
  patch -p1 -i ${srcdir}/0010-fix-compare-ints.patch
}

build() {
  export OPENIMAGEIOHOME=${MINGW_PREFIX}
  export LLVM_DIRECTORY=${MINGW_PREFIX}
  [[ -d build-${MINGW_CHOST} ]] && rm -r build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DLLVM_STATIC=1 \
    -DCMAKE_CXX_FLAGS="-DTINYFORMAT_ALLOW_WCHAR_STRINGS -Wno-error=unused-but-set-variable -std=gnu++11" \
    -DEXTRA_OSLEXEC_LIBRARIES="-lz -lffi -lncurses -lshell32 -lpsapi -limagehlp -lm" \
    ../$_realname
    #-DVERBOSE=1

   #     -DILMBASE_CUSTOM=True \
   # -DILMBASE_CUSTOM_INCLUDE_DIR=${MINGW_PREFIX}/include \
   # -DILMBASE_CUSTOM_LIB_DIR=${MINGW_PREFIX}/lib \
   # -DILMBASE_CUSTOM_LIBRARIES="Half Iex Imath IlmThread" \
  # $PWD/src/liboslcomp PATH addition needed for running oslc.exe during the build (for liboslcomp.dll)
  # ${MINGW_PREFIX}/bin PATH addition needed for finding libboost_system-mt.dll when running oslc.exe.
  #PATH=$PWD/src/liboslcomp:${MINGW_PREFIX}/bin:"${PATH}" make VERBOSE=1
  PATH="$PWD/src/liboslcomp:${PATH}" make
}

package() {
  cd build-${MINGW_CHOST}

  make install

  mkdir -p "${pkgdir}${MINGW_PREFIX}"/share/OSL/
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}
  mv "${pkgdir}${MINGW_PREFIX}"/LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname} # TODO: Tell upstream about this shit
  mv "${pkgdir}${MINGW_PREFIX}"/{CHANGES,README.md,INSTALL} "${pkgdir}${MINGW_PREFIX}"/share/OSL/
  mv "${pkgdir}${MINGW_PREFIX}"/doc "${pkgdir}${MINGW_PREFIX}"/share/OSL/doc
  #mv "${pkgdir}${MINGW_PREFIX}"/shaders "${pkgdir}${MINGW_PREFIX}"/share/OSL/shadersE
}
