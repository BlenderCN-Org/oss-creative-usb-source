# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libsoup
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.48.0
pkgrel=1
pkgdesc="HTTP client/server library (mingw-w64)"
arch=(any)
url="http://ftp.gnome.org/pub/GNOME/sources/libsoup"
license=("LGPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "${MINGW_PACKAGE_PREFIX}-gobject-introspection" "intltool")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-glib2"
        "${MINGW_PACKAGE_PREFIX}-libxml2"
        "${MINGW_PACKAGE_PREFIX}-sqlite3"
        "${MINGW_PACKAGE_PREFIX}-glib-networking")
options=('staticlibs' 'strip')
source=("http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz"
        0001-give-cc-to-gir-scanner.mingw.patch
        0002-fix-keyword-conflict.patch
        0003-fix-missing-symbol.patch)
md5sums=('51ccb76f5262945e1f0316fdc46de567'
         'a1ddc19ab3f73bbddda1d41a6a7de399'
         '6b449ba6f360871f18577e6b3e90b8fe'
         'da12af3cca187a76f5493c120c4ed79f')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  patch -p1 -i ${srcdir}/0001-give-cc-to-gir-scanner.mingw.patch
  patch -p1 -i ${srcdir}/0002-fix-keyword-conflict.patch
  patch -p1 -i ${srcdir}/0003-fix-missing-symbol.patch
  autoreconf -fi
}

build() {
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  "${srcdir}"/${_realname}-$pkgver/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --without-apache-httpd \
    --without-apache-module-dir \
    --with-gnome \
    --enable-introspection
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
}
