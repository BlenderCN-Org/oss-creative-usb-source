# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=h5py
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-$_realname"
         "${MINGW_PACKAGE_PREFIX}-python3-$_realname")
pkgver=2.3.1
pkgrel=1
pkgdesc="General-purpose Python bindings for the HDF5 library. (mingw-w64)"
arch=('any')
url="http://www.h5py.org"
license=('BSD')
makedepends=(
    "${MINGW_PACKAGE_PREFIX}-python2-numpy"
    "${MINGW_PACKAGE_PREFIX}-python3-numpy"
    "${MINGW_PACKAGE_PREFIX}-cython"
    "${MINGW_PACKAGE_PREFIX}-cython2"
    "${MINGW_PACKAGE_PREFIX}-gcc"
    "${MINGW_PACKAGE_PREFIX}-hdf5"
)
source=(
  https://pypi.python.org/packages/source/h/h5py/h5py-$pkgver.tar.gz
  setup.py.patch
  lzf_c.c.patch
)
md5sums=('8f32f96d653e904d20f9f910c6d9dd91'
         '07604b410e29df82c06a9add19b755a5'
         '8c28048812d378a8a9330dbd21def864')

prepare() {
  cd $_realname-$pkgver
  patch -p1 -i ${srcdir}/setup.py.patch
  patch -p1 -i ${srcdir}/lzf_c.c.patch
  cd ..

  cp -a $_realname-$pkgver $_realname-py2-$pkgver
  cp -a $_realname-$pkgver $_realname-py3-$pkgver
}

build() {
  # build for python2
  cd ${srcdir}/$_realname-py2-$pkgver

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py build

  # build for python3
  cd ${srcdir}/$_realname-py3-$pkgver

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py build
}

package_python2-h5py() {
  depends=(
    "${MINGW_PACKAGE_PREFIX}-python2-numpy"
    "${MINGW_PACKAGE_PREFIX}-hdf5"
  )

  cd $_realname-py2-$pkgver
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --skip-build \
  --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1

  install -Dm644 licenses/license.txt \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE.txt"
}

package_python3-h5py() {
  depends=(
    "${MINGW_PACKAGE_PREFIX}-python3-numpy"
    "${MINGW_PACKAGE_PREFIX}-hdf5"
  )

  cd $_realname-py3-$pkgver
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --skip-build \
  --prefix=${MINGW_PREFIX} --root=${pkgdir} --optimize=1

  install -Dm644 licenses/license.txt \
    "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE.txt"
}

package_mingw-w64-i686-python2-h5py() {
  package_python2-h5py
}

package_mingw-w64-i686-python3-h5py() {
  package_python3-h5py
}

package_mingw-w64-x86_64-python2-h5py() {
  package_python2-h5py
}

package_mingw-w64-x86_64-python3-h5py() {
  package_python3-h5py
}
