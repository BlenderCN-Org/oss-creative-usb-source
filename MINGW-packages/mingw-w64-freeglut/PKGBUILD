# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=freeglut
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pre=rc1
_ver=3.0.0
pkgver=${_ver}${_pre}
pkgrel=1
pkgdesc="Freeglut allows the user to create and manage windows containing OpenGL contexts (mingw32-w64)"
arch=('any')
url="http://freeglut.sourceforge.net/"
license=('MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' '!libtool' 'staticlibs')
source=("http://downloads.sourceforge.net/freeglut/${_realname}-${_ver}-${_pre}.tar.gz")
md5sums=('9679f19e5d9fd211c904698be0cfec2b')

build() {
  rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    ../${_realname}-${_ver}-${_pre}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make install
}
