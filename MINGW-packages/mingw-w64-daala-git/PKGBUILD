# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=daala
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgdesc="Daala is next-next-gen video compression technology from Xiph.org, Mozilla and others."
pkgver=r1109.07ba88c
pkgrel=1
arch=('any')
url="https://www.xiph.org/daala/"
license=('BSD')
#groups=("${MINGW_PACKAGE_PREFIX}")
makedepends=('git' "${MINGW_PACKAGE_PREFIX}-check")
depends=("${MINGW_PACKAGE_PREFIX}-libogg"
         "${MINGW_PACKAGE_PREFIX}-libpng")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
options=('strip' 'staticlibs')
source=("${_realname}"::"git+https://git.xiph.org/daala.git"
        0001-make-daalabase-public.mingw.patch
        0002-make-daalabase-public.mingw.patch)
md5sums=('SKIP'
         'b6aae57a2a95c785aa7b94f67c428b51'
         '901557d36c62ccebf40d508ad834f157')

pkgver() {
  cd "$srcdir/$_realname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${_realname}
  patch -p1 -i ${srcdir}/0001-make-daalabase-public.mingw.patch
  patch -p1 -i ${srcdir}/0002-make-daalabase-public.mingw.patch

  autoreconf -fi
}

build() {
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-shared \
    --enable-static \
    --enable-tools \
    --disable-unit-tests
   make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${_realname}/COPYING ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING
}
