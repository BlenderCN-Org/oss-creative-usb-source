# Contributor: Runar Tenfjord < runar dot tenfjord at gmail dot com >

_realname=pytz
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-$_realname" "${MINGW_PACKAGE_PREFIX}-python3-$_realname")
pkgver=2014.7
pkgrel=1
pkgdesc="Cross platform time zone library for Python (mingw-w64)"
arch=('any')
url="http://pypi.python.org/pypi/pytz"
license=("MIT")
makedepends=(
            "${MINGW_PACKAGE_PREFIX}-python3"
            "${MINGW_PACKAGE_PREFIX}-python2"
)
source=(http://pypi.python.org/packages/source/p/pytz/pytz-$pkgver.tar.bz2)
md5sums=('5c1d3966406c6637b95d1eb2652cd8e1')

prepare() {
  cd "${srcdir}"
  cp -r pytz-$pkgver python2-pytz-$pkgver
  cp -r pytz-$pkgver python3-pytz-$pkgver
}

check(){
    cd $srcdir/python2-pytz-$pkgver/pytz/tests
    ${MINGW_PREFIX}/bin/python2 test_tzinfo.py
    cd $srcdir/python3-pytz-$pkgver/pytz/tests
    ${MINGW_PREFIX}/bin/python3 test_tzinfo.py
}

package_python3-pytz() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd $srcdir/python3-pytz-$pkgver
  # Fix locale https://github.com/ipython/ipython/issues/2057
  export LC_ALL=en_US.UTF-8

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
  --root=$pkgdir --optimize=1

  install -D LICENSE.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE
}

package_python2-pytz() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd $srcdir/python2-pytz-$pkgver
  # Fix locale https://github.com/ipython/ipython/issues/2057
  export LC_ALL=en_US.UTF-8

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
  --root=$pkgdir --optimize=1

  install -D LICENSE.txt ${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE
}

package_mingw-w64-i686-python2-pytz() {
  package_python2-pytz
}

package_mingw-w64-i686-python3-pytz() {
  package_python3-pytz
}

package_mingw-w64-x86_64-python2-pytz() {
  package_python2-pytz
}

package_mingw-w64-x86_64-python3-pytz() {
  package_python3-pytz
}
