# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=ghex
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=3.10.1
pkgrel=2
arch=('any')
pkgdesc="GNOME Hex editor for files (mingw-w64)"
options=(strip staticlibs)
depends=("${MINGW_PACKAGE_PREFIX}-gtk3"
        "${MINGW_PACKAGE_PREFIX}-adwaita-icon-theme")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
license=("GPL 2")
url="http://www.gnome.org"
install=ghex-${CARCH}.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        001-nohelp.patch
        002-fread.patch
        004-localedir-fix.patch)
sha256sums=('34b66cb5c84410c420df72f229d25aee5979e58048a246ed719b046f0c241132'
            'cd87d0040cd37a942ff8a1e3013bc96083920dc959dac0a96b543b8b706585ff'
            '8e403c1e7c3f83adc768bfe45bb1ea42ae90fc4dfb7fe9bf4544820541a3241d'
            '658ed0355d655372b8a1b1fc30a45ce4f02abbad51478132ed08d5373f9514d2')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p0 -i ${srcdir}/001-nohelp.patch
  patch -p0 -i ${srcdir}/002-fread.patch
  patch -p0 -i ${srcdir}/004-localedir-fix.patch

  autoreconf -ifv
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
