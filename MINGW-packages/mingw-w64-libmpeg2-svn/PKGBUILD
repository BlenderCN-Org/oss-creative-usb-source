# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libmpeg2

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-svn"
pkgver=r1206
pkgrel=1
pkgdesc="Library for decoding MPEG-1 and MPEG-2 video streams (mingw-w64)"
arch=('any')
url="http://libmpeg2.sourceforge.net/"
license=("GPL2")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "subversion")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
options=('staticlibs' 'strip')
source=("${_realname}"::"svn+svn://svn.videolan.org/libmpeg2/trunk"
        0001-fix-bootstrap.mingw.patch
        0002-libmpeg2-fix-deprecated.patch)
sha256sums=('SKIP'
            '0ba11aa8ee1aa185dec49790d6c12ed364345fe2ea78616caafa96f1cbd91766'
            '4dca32a82a5845f4fcbc26d79ae4fdc5d9ab27cca5c2ae5ea5fa9078a0077eb6')

pkgver() {
  cd "$srcdir/$_realname"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd ${srcdir}/${_realname}
  patch -p1 -i ${srcdir}/0001-fix-bootstrap.mingw.patch
  patch -p1 -i ${srcdir}/0002-libmpeg2-fix-deprecated.patch

  ./bootstrap
}

build() {
  cd "${srcdir}/${_realname}"
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-shared \
    --enable-static \
    --disable-sdl
  make
}

package() {
  cd "${srcdir}/${_realname}"
  make DESTDIR="$pkgdir" install
}
