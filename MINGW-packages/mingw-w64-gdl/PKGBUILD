# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer (MSYS2): Ray Donnelly <mingw.android@gmail.com>

_realname=gdl
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=3.14.0
pkgrel=1
pkgdesc="GNOME Docking Library (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.gnome.org/"
depends=("${MINGW_PACKAGE_PREFIX}-gtk3")
makedepends=("${MINGW_PACKAGE_PREFIX}-gtk-doc"
             "intltool"
             "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
             "${MINGW_PACKAGE_PREFIX}-python2")
options=('staticlibs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${_realname}/${pkgver:0:4}/${_realname}-${pkgver}.tar.xz)
sha256sums=('4b903c28a8894a82b997a1732a443c8b1d6a510304b3c3b511023339ff5d01db')

build() {
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir "${srcdir}"/build-${CARCH}
  cd "${srcdir}"/build-${CARCH}
  PYTHON=${MINGW_PREFIX}/bin/python2 \
    "${srcdir}"/${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --{build,host}=${MINGW_HOST} \
      --sysconfdir=${MINGW_PREFIX}/etc \
      --localstatedir=${MINGW_PREFIX}/var \
      --enable-static \
      --enable-shared
  make CC=${MINGW_PREFIX}/bin/gcc
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make DESTDIR="$pkgdir" install
}
