# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=openldap
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.4.40
pkgrel=1
arch=('any')
pkgdesc="OpenLDAP Software is an open source implementation of the Lightweight Directory Access Protocol (only client) (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-cyrus-sasl"
         "${MINGW_PACKAGE_PREFIX}-icu"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-db<6.0.20")
makedepends=("groff"
            "${MINGW_PACKAGE_PREFIX}-gcc"
            "libintl"
            "libtool"
            "make")
options=('strip' 'staticlibs' '!makeflags')
license=("The OpenLDAP Public License")
url="http://www.openldap.org/"
source=(http://www.openldap.org/software/download/OpenLDAP/openldap-release/${_realname}-$pkgver.tgz
        001-backmdb.patch
        002-link-with-icu.patch
        003-symdummy-error.patch)
sha256sums=('d12611a5c25b6499293c2bb7b435dc2b174db73e83f5a8cb7e34f2ce5fa6dadb'
            'b462bfab9362cf5cab61d67cd24902a1800fb498a152b681ee182c713b79146c'
            '450cc9e42fb6ed2af4a16b217c77f95d1a18b353b887cab3107f9b350d8b42c5'
            'd697fe51361a5129a5af3ca317031ff6f4ca5ddd247ff53043880660154a4708')

prepare() {
  cd ${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-backmdb.patch
  patch -p1 -i ${srcdir}/002-link-with-icu.patch
  patch -p1 -i ${srcdir}/003-symdummy-error.patch

  #libtoolize --force --copy
  #AUTOMAKE=/usr/bin/true autoreconf -fi
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  unset CPPFLAGS
  export lt_cv_deplibs_check_method='pass_all'
  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --sbindir=${MINGW_PREFIX}/bin \
    --with-tls=openssl \
    --with-cyrus-sasl \
    --enable-modules=yes \
    CC=${MINGW_PREFIX}/bin/gcc
  sed -i "s/#define socklen_t int/\/*#define socklen_t int*\//" include/portable.h
  make depend
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
