# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

_realname=nspr
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.10.8
pkgrel=1
pkgdesc="Netscape Portable Runtime (mingw-w64)"
arch=('any')
url="http://www.mozilla.org/projects/nspr/"
license=(MPL2)
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "zip")
options=('staticlibs' 'strip' '!emptydirs')
source=(ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${_realname}-${pkgver}.tar.gz
        http://ftp.mozilla.org/pub/mozilla.org/mozilla/libraries/win32/moztools-static.zip
        nspr-4.10.6-mswsock.patch
        nspr-4.10.2_autotools.patch
        nspr-4.10.2_mingw.patch
        nspr-4.10.2_staticbuild.patch
        nspr-4.10.2_x64.patch
        manifest)
md5sums=('8d7c5bd0a5b0a7d0e705be66479030a0'
         'd9c6516eb4dca209e4b7ae46a4758ffa'
         '5d99dd3b898f615b58d111f7b55a4217'
         'ed16729657eb70fbacda4f086c7a04ed'
         '93d379011339c994fd242c9628df0bdf'
         'e8b657f34a43d60db96761a924c46a9c'
         'c337834e9092cffa3cd448c0702efd82'
         '9634847026a86bbfc395c7be23d30458')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}/nspr
  patch -p1 -i ${srcdir}/nspr-4.10.6-mswsock.patch
  patch -p0 -i ${srcdir}/nspr-4.10.2_autotools.patch
  patch -p0 -i ${srcdir}/nspr-4.10.2_mingw.patch
  patch -p0 -i ${srcdir}/nspr-4.10.2_staticbuild.patch
  patch -p0 -i ${srcdir}/nspr-4.10.2_x64.patch

  cp ${srcdir}/moztools/bin/nsinstall.exe ../
  cp ${srcdir}/manifest ../nsinstall.exe.manifest
}

build() {
  #mkdir -p ${srcdir}/build-${MINGW_CHOST}
  #cd ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/${_realname}-${pkgver}
  if [ "$CARCH" = "x86_64" ]; then
    _confflags="--enable-64bit"
  else
    _confflags=""
  fi

  ./nspr/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --includedir=${MINGW_PREFIX}/include/nspr \
    --enable-optimize \
    --disable-debug \
    ${_confflags}

  PATH="$PATH:${srcdir}/${_realname}-${pkgver}" make
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  PATH="$PATH:${srcdir}/${_realname}-${pkgver}" make DESTDIR="$pkgdir" install

  mv ${pkgdir}${MINGW_PREFIX}/lib/*.dll ${pkgdir}${MINGW_PREFIX}/bin/

  ln -s nspr.pc "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/mozilla-nspr.pc"
  rm -r ${pkgdir}${MINGW_PREFIX}/bin/{compile-et.pl,prerr.properties} \
        ${pkgdir}${MINGW_PREFIX}/include/nspr/md

  install -Dm644 nspr/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
