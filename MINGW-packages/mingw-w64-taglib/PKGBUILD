# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=taglib

pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.9.1
pkgrel=3
pkgdesc="A Library for reading and editing the meta-data of several popular audio formats (mingw-w64)"
arch=('any')
url="http://taglib.github.com"
license=("GPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-cppunit")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs" "${MINGW_PACKAGE_PREFIX}-zlib")
options=('strip' 'staticlibs')
source=(http://taglib.github.io/releases/${_realname}-${pkgver}.tar.gz
        fix-finding-cppunit.patch
        fix-redefinition-NOMINMAX.patch
        mingw-pkg-config.patch)
md5sums=('0d35df96822bbd564c5504cb3c2e4d86'
         '35acd6a8db64597d01eff718a4279e41'
         '55421ca09d4f9d8bc426362d529b95aa'
         'e4c69d1ca60ec85b20776339f9094d3a')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/fix-finding-cppunit.patch
  patch -p1 -i ${srcdir}/fix-redefinition-NOMINMAX.patch
  patch -p1 -i ${srcdir}/mingw-pkg-config.patch
}

build() {
  msg "Build shared version"
  mkdir -p "${srcdir}/build-shared-${MINGW_CHOST}"
  cd "${srcdir}/build-shared-${MINGW_CHOST}"

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DENABLE_STATIC:BOOL=OFF \
    -DWITH_ASF:BOOL=ON \
    -DWITH_MP4:BOOL=ON \
    ../${_realname}-${pkgver}

  make

  msg "Build static version"
  mkdir -p "${srcdir}/build-static-${MINGW_CHOST}"
  cd "${srcdir}/build-static-${MINGW_CHOST}"

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DENABLE_STATIC:BOOL=ON \
    -DWITH_ASF:BOOL=ON \
    -DWITH_MP4:BOOL=ON \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-static-${MINGW_CHOST}"
  make install

  cd "${srcdir}/build-shared-${MINGW_CHOST}"
  make install

  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/taglib.pc
}
