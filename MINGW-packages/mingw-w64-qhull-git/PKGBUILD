# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: JÃ¼rgen Pfeifer <juergen@familiepfeifer.de>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=qhull
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-git"
pkgrel=2
pkgver=r113.60d5581
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgdesc="A general dimension code for computing convex hulls and related structures (mingw-w64)"
arch=('any')
url="http://www.${_realname}.org/"
license=('custom')
groups=("${MINGW_PACKAGE_PREFIX}")
options=('strip' 'staticlibs')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
makedepends=("git"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-cmake")
source=("$_realname"::"git://gitorious.org/qhull/qhull.git"
        "0001-MSYS2-Adopt-FHS-layout-in-cmake.patch"
        "0002-MinGW-w64-64-bit-ptr_intT-fix.patch")
sha1sums=('SKIP'
          'bfd74f4b8e69100d75e8777554cca2d32b4bad94'
          '5494fe7a909fda8842ee1a7c2771cdefb52eda6f')

pkgver() {
  cd "${srcdir}"/$_realname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/$_realname
  git am "${srcdir}"/0001-MSYS2-Adopt-FHS-layout-in-cmake.patch
  git am "${srcdir}"/0002-MinGW-w64-64-bit-ptr_intT-fix.patch
}

build() {
  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
    export PREFIX_WIN=`pwd -W`
  popd > /dev/null

  pushd ${MINGW_PREFIX} > /dev/null
    export PREFIX_DEPS=`pwd -W`
  popd > /dev/null

  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}
  ${MINGW_PREFIX}/bin/cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${PREFIX_WIN} \
    -DBUILD_SHARED_LIBS=ON \
    "${srcdir}"/$_realname
  make
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install # DESTDIR="$pkgdir"
  pushd ${pkgdir}${MINGW_PREFIX}/lib >/dev/null
  if [ -f lib${_realname}static.a ]; then
    mv -f lib${_realname}static.a lib${_realname}.a
  fi
  if [ -f lib${_realname}static_p.a ]; then
    mv -f lib${_realname}static_p.a lib${_realname}_p.a
  fi
  popd >/dev/null
  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname
  cp -pf "${srcdir}"/${_realname}/COPYING* \
    ${pkgdir}${MINGW_PREFIX}/share/licenses/$_realname
}
