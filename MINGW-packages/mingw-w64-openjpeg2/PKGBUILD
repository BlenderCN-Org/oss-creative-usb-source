# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openjpeg
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}2"
pkgver=2.1.0
pkgrel=2
pkgdesc="An open source JPEG 2000 codec (mingw-w64)"
arch=('any')
url="http://www.openjpeg.org"
license=("BSD")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cmake")
options=('staticlibs' 'strip')
source=("http://sourceforge.net/projects/${_realname}.mirror/files/${pkgver}/${_realname}-${pkgver}.tar.gz"
        0001-fix-install-for-dlls.all.patch
        0003-versioned-dlls.mingw.patch
        0005-sock-jpip.all.patch
        0006-openjpeg-2.1.0-stdcall-for-all-win.patch)

md5sums=('f6419fcc233df84f9a81eb36633c6db6'
         '9df59d9482605662734934fa02129b6c'
         'fe2141975de480664c1d96e365b4b29a'
         'f4f5174de862b6803702d6eaa83da4da'
         '46704c1ed92e81dbe9e341872b4ef261')

prepare() {
  cd "$srcdir/${_realname}-$pkgver"
  patch -p1 -i ${srcdir}/0001-fix-install-for-dlls.all.patch
  patch -p1 -i ${srcdir}/0003-versioned-dlls.mingw.patch
  patch -p1 -i ${srcdir}/0005-sock-jpip.all.patch
  patch -p1 -i ${srcdir}/0006-openjpeg-2.1.0-stdcall-for-all-win.patch
}

build() {
  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null

  msg "Build static version"
  [[ -d $srcdir/build-static-${MINGW_CHOST} ]] && rm -rf $srcdir/build-static-${MINGW_CHOST}
  mkdir $srcdir/build-static-${MINGW_CHOST}
  cd $srcdir/build-static-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=OFF \
    -DBUILD_TESTING:BOOL=OFF \
    -DCMAKE_SYSTEM_PREFIX_PATH=${PREFIX_DEPS} \
    -DBUILD_MJ2:bool=ON \
    -DBUILD_JPWL:bool=ON \
    -DBUILD_JPIP:bool=OFF \
    -DBUILD_JP3D:bool=ON \
    ../${_realname}-$pkgver
  make

  msg "Build shared version"
  [[ -d $srcdir/build-shared-${MINGW_CHOST} ]] && rm -rf $srcdir/build-shared-${MINGW_CHOST}
  mkdir $srcdir/build-shared-${MINGW_CHOST}
  cd $srcdir/build-shared-${MINGW_CHOST}

  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTING:BOOL=ON \
    -DCMAKE_SYSTEM_PREFIX_PATH=${PREFIX_DEPS} \
    -DBUILD_MJ2:bool=ON \
    -DBUILD_JPWL:bool=ON \
    -DBUILD_JPIP:bool=ON \
    -DBUILD_JP3D:bool=ON \
    -DBUILD_DOC:bool=OFF \
    -DBUILD_PKGCONFIG_FILES=ON \
    ../${_realname}-$pkgver
  make
}

package() {
  cd "$srcdir/build-static-${MINGW_CHOST}"
  make install

  cd "$srcdir/build-shared-${MINGW_CHOST}"
  make install

  mkdir -p ${pkgdir}${MINGW_PREFIX}
  pushd ${pkgdir}${MINGW_PREFIX} > /dev/null
  export PREFIX_WIN=`pwd -W`
  popd > /dev/null
  sed -s "s|${PREFIX_WIN}|${MINGW_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libopenjp2.pc \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libopenjp3d.pc \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libopenjpip.pc \
    -i ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libopenjpwl.pc

}
