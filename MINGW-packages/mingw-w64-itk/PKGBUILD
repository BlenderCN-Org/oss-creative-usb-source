# Maintainer: Sergey Popov <popov.sa@gmail.com>

_realname=itk
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.6.1
pkgrel=1
pkgdesc="An open-source C++ toolkit for medical image processing (mingw-w64)"
arch=('any')
url="http://www.itk.org/"
license=("Apache" "GPL")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
        "${MINGW_PACKAGE_PREFIX}-expat"
        "${MINGW_PACKAGE_PREFIX}-fftw"
        "${MINGW_PACKAGE_PREFIX}-hdf5"
        "${MINGW_PACKAGE_PREFIX}-libjpeg"
        "${MINGW_PACKAGE_PREFIX}-libpng"
        "${MINGW_PACKAGE_PREFIX}-libtiff"
        "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-cmake"
            "${MINGW_PACKAGE_PREFIX}-opencv"
            "${MINGW_PACKAGE_PREFIX}-vtk")
optdepends=("${MINGW_PACKAGE_PREFIX}-opencv: ITK-OpenCV bridge"
           "${MINGW_PACKAGE_PREFIX}-vtk: ITK-VTK bridge")
source=(https://github.com/InsightSoftwareConsortium/ITK/archive/v$pkgver.tar.gz)
md5sums=('50993b721839f22c8b7a4d76bab23adc')

build() {
  CXXFLAGS+=" -std=gnu++11"
  [[ "$CARCH" = 'i686' ]] && {
    _cmakeopts=('-DITK_USE_64BITS_IDS=OFF' '-DVNL_CONFIG_ENABLE_SSE2_ROUNDING=OFF')
  }
  [[ "$CARCH" = 'x86_64' ]] && {
    _cmakeopts=('-DITK_USE_64BITS_IDS=ON' '-DVNL_CONFIG_ENABLE_SSE2_ROUNDING=ON')
  }

  [[ -d ${srcdir}/build-${CARCH} ]] && rm -rf ${srcdir}/build-${CARCH}
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}

  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${pkgdir}${MINGW_PREFIX} \
    -DBUILD_DOCUMENTATION=OFF \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TESTING=OFF \
    -DITK_LEGACY_REMOVE=OFF \
    -DVNL_CONFIG_THREAD_SAFE=ON \
    -DModule_ITKVideoBridgeOpenCV=ON \
    -DModule_ITKVtkGlue=ON \
    -DITK_USE_GPU=OFF \
    -DITK_USE_SYSTEM_EXPAT=ON \
    -DITK_USE_SYSTEM_FFTW=ON \
    -DUSE_FFTWD=ON \
    -DUSE_FFTWF=ON \
    -DITK_USE_SYSTEM_GDCM=OFF \
    -DITK_USE_SYSTEM_HDF5=ON \
    -DITK_USE_SYSTEM_JPEG=ON \
    -DITK_USE_SYSTEM_PNG=ON \
    -DITK_USE_SYSTEM_TIFF=ON \
    -DITK_USE_SYSTEM_VXL=OFF \
    -DITK_USE_SYSTEM_ZLIB=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    ${_cmakeopts[@]} \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd "${srcdir}/build-${CARCH}"
  make -j1 install

  pushd ${MINGW_PREFIX} > /dev/null
  export PREFIX_DEPS=`pwd -W`
  popd > /dev/null

  ITK_SUFFIX=$(echo "${pkgver}" | sed 's|\([0-9]*\.[0-9]*\).*$|\1|')

  sed -s "s|${PREFIX_DEPS}|\${_IMPORT_PREFIX}|g" \
    -i ${pkgdir}${MINGW_PREFIX}/lib/cmake/${_realname}-${ITK_SUFFIX}/ITKTargets-release.cmake
  find "${pkgdir}${MINGW_PREFIX}/lib/cmake/${_realname}-${ITK_SUFFIX}" -name '*.cmake' -exec \
    sed -s "s|${PREFIX_DEPS}|\${ITK_INSTALL_PREFIX}|g" -i {} \;
}
