# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=ag
_longname=the_silver_searcher
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.27.0
pkgrel=1
pkgdesc="The Silver Searcher: An attempt to make something better than ack, which itself is better than grep (mingw-w64)"
arch=('any')
url="http://geoff.greer.fm/ag"
license=("Apache")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-pcre" "${MINGW_PACKAGE_PREFIX}-xz" "${MINGW_PACKAGE_PREFIX}-zlib")
options=('strip' 'staticlibs')
source=(http://geoff.greer.fm/ag/releases/$_longname-$pkgver.tar.gz
        001-mingw-build.patch)
md5sums=('56ba98da4298e9db1929f34ca05b1627'
         '0dbfdf4ddba43446de0f4941844dfcfd')

prepare() {
  cd "${srcdir}/${_longname}-${pkgver}"
  patch -p1 -i ${srcdir}/001-mingw-build.patch

  autoreconf -fi
}

build() {

  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  ../${_longname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
