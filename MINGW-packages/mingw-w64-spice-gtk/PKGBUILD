# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=spice-gtk
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.27
pkgrel=1
arch=('any')
pkgdesc="GTK3 widget for SPICE clients (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-cyrus-sasl"
         "${MINGW_PACKAGE_PREFIX}-dbus-glib"
         "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-lz4"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-phodav"
         "${MINGW_PACKAGE_PREFIX}-pixman"
         "${MINGW_PACKAGE_PREFIX}-spice-protocol"
         "${MINGW_PACKAGE_PREFIX}-usbredir"
         "${MINGW_PACKAGE_PREFIX}-vala")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             #"${MINGW_PACKAGE_PREFIX}-python2-pygtk"
             "${MINGW_PACKAGE_PREFIX}-python2")
options=('strip' 'staticlibs')
license=("GPL 2")
url="http://www.spice-space.org"
source=(http://www.spice-space.org/download/gtk/${_realname}-$pkgver.tar.bz2
        001-win.patch
        002-lz4-support.patch
        003-python-linking.patch)
sha256sums=('5075e07010db434146649110ade31f5a6cdcdb4efbb4bedd40f1a977f090430c'
            'b607df1fb112bb21d174833eb549f7295df9898104755c27da717106ed52eaab'
            '8d1d3290345bfa61a31780d8dbaf0e0b3663c1eea0dd10af32929b6a0612deb0'
            '4ce293c1e6821f5316a52e87cbc5a03ef8595c7feef472cad0d16f1145bdbaa6')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/001-win.patch
  patch -p1 -i ${srcdir}/002-lz4-support.patch
  patch -p1 -i ${srcdir}/003-python-linking.patch

  autoreconf -isf
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --enable-introspection \
    --enable-vala \
    --enable-lz4 \
    --with-gtk=3.0 \
    --without-python \
    --with-audio=no

  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
