# Maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname=libgdata
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.16.1
pkgrel=1
arch=('any')
pkgdesc="Library for accessing GData webservices (mingw-w64)"
depends=("${MINGW_PACKAGE_PREFIX}-glib2"
         "${MINGW_PACKAGE_PREFIX}-libsoup"
         "${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-gtk3"
         "${MINGW_PACKAGE_PREFIX}-gdk-pixbuf2"
         "${MINGW_PACKAGE_PREFIX}-liboauth"
         "${MINGW_PACKAGE_PREFIX}-json-glib"
         "${MINGW_PACKAGE_PREFIX}-uhttpmock")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
            "${MINGW_PACKAGE_PREFIX}-gobject-introspection"
            "${MINGW_PACKAGE_PREFIX}-pkg-config"
            "make"
            "libtool"
            "intltool"
            "automake-wrapper"
            "autoconf"
            "patch")
options=('strip' 'staticlibs')
license=("LGPL 2.1")
url="http://www.gnome.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/${pkgver%.*}/${_realname}-$pkgver.tar.xz
        0001-give-cc-to-gir-scanner.all.patch
        0002-symbols.patch
        0003-wsock.patch
        0004-localedir-fix.patch)
sha256sums=('8740e071ecb2ae0d2a4b9f180d2ae5fdf9dc4c41e7ff9dc7e057f62442800827'
            '2184839b01d19afebf10b8ca91c79682b834c9e812e0fdeb683c69b851689d1f'
            '3f9d5f53a2d1be337686a83d3b33f111e294dcbc4f78ed1facbfb048f6c5973a'
            'ed4c9c441ee3bf4a5279c5d212b2c1116ce443763151b71975611406310575df'
            '0d035c44f80b0bfb6ed28b14dc908bf98ac11c943a99281cd93c5d8ed26b51a8')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/0001-give-cc-to-gir-scanner.all.patch
  patch -p1 -i ${srcdir}/0002-symbols.patch
  patch -p1 -i ${srcdir}/0003-wsock.patch
  patch -p1 -i ${srcdir}/0004-localedir-fix.patch

  autoreconf -fi
}

build() {
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --build=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --libexecdir=${MINGW_PREFIX}/lib \
    --disable-gnome
  make
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  make DESTDIR=$pkgdir install
}
