# Maintainer: Ray Donnelly <mingw.android@gmail.com>

_realname=libplist
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.12
pkgrel=2
pkgdesc='A library to handle Apple Property List format in binary or XML (mingw-w64)'
groups=("${MINGW_PACKAGE_PREFIX}" "xdev")
arch=('any')
url='http://www.libimobiledevice.org/'
license=('GPL2+')
source=("http://www.libimobiledevice.org/downloads/${_realname}-${pkgver}.tar.bz2"
        001-cython-module.patch)
depends=("${MINGW_PACKAGE_PREFIX}-libxml2"
         "${MINGW_PACKAGE_PREFIX}-cython")
options=('staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3")
sha1sums=('48f071c723387637ef2cc2980d4ca0627aab067b'
          '974595d36c8ddfcf97aab0d3f2b4ee36b1440351')

prepare() {
  cd $srcdir/${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/001-cython-module.patch

  autoreconf -fiv
}

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  [ -d "${srcdir}"/build-${CARCH} ] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

  PYTHON=${MINGW_PREFIX}/bin/python3 \
    ../${_realname}-${pkgver}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST} \
      --enable-static \
      --enable-shared
  make
}

check() {
  cd "${srcdir}"/build-${CARCH}
  # || true because of FAIL: bigarraycmp .. File does not exists
  make check || true
}

package() {
  cd "${srcdir}"/build-${CARCH}
  make install DESTDIR="${pkgdir}"

  rm -f ${pkgdir}${MINGW_PREFIX}/lib/python*/site-packages/*.a
}
